/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl 

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


rule "Hib: Recommend Hib Conditional High Risk if Patient Age >=5yrs and Series Not Complete by 5yrs of Age and Cancel all other Recommendation Rules" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- make note of the number of doses required to complete this series as $assign_nNumberOfDosesRequired
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $assign_dtDateAtAge when the patient is "5y" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

// Exception 1
rule "Hib: Include Recommendation with Date 7months in 4-Dose Hib Series and TargetDose 2 if Patient is between 7and12months and Received No Doses before 7months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the effective dose number in the Series is <= 2
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeSevenMonths when the Patient is "7m" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeSevenMonths is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeSevenMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 2 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

// Exception 2A
rule "Hib: Include Recommendation with Date 12months and TargetDose 3 in 4-Dose Hib Series if Patient between age 12and15months and Received <2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the effective dose number in the Series is <= 3
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age	
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is < 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "15m"
	then
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 3 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

// Exception 2B
rule "Hib: Set Recommendation Date to 12months and TargetDose 4 in 4-Dose Hib Series and Patient between age 12and15months and Received 2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the effective dose number in the Series is <= 4
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age
		There is a Series $sameTargetSeries identified by $targetSeries
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is == 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "15m"
	then
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

// Exception 3
rule "Hib: Set Recommendation Date to 15months and TargetDose 4 in 4-Dose Hib Series if Patient between age 15months and 5years" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the effective dose number in the Series is <= 4
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeFifteenMonths when the Patient is "15m" of age					
		There is a Series $sameTargetSeries identified by $targetSeries
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $dtDateAtAgeFifteenMonths is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and evalTime >= "15m" && elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Include a Recommendation as $recommendation with Recommended Forecast Date $dtDateAtAgeFifteenMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

