/**
 * Copyright (C) 2023 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.Date
import java.util.Iterator
import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdata.BaseDataEvaluationReason
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime


rule "Orthopoxvirus: If the administered vaccine is not permitted by default for the series, and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.860"
			- the series that the shot belongs to is not complete
			- make note of the administered vaccine as $vaccineAdministered
			- make note of the dose number as $doseNumber
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the vaccine $vaccineAdministered is not permitted for dose number $doseNumber in this series
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Orthopoxvirus: If the dose 2 of the 2-dose series is administered prior to 24 days, include supplemental text that the dose does not follow the guidelines for minimum intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.860"
			- the series is complete
			- the name of the Series is "Orthopoxvirus2DoseSeries"
			- the effective number of doses administered in the Series is == 2
		There is an administered shot $administeredShot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity Status is VALID
			- the dose number in the Series is == 2
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $currentShotDate
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		Confirm elapsed time between $priorShotDate and $currentShotDate < "24d"
	then
		Include Supplemental Text "The interval between this dose and the prior shot is less than the absolute minimum interval. The dose does not need to be repeated unless the patient is severely immunosuppressed, in which case the repeated dose should be administered after the dose given in error by at least 28 days." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
        Log that this dose rule fired for the dose $administeredShot in the Series $targetSeries
end


rule "Orthopoxvirus: If a booster dose is administered, evaluate the shot as Valid"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.860"
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- make note of the dose number after which the Series was marked complete as $doseNumberAfterWhichComplete
			- the effective number of doses administered in the Series is < $doseNumberAfterWhichComplete+1
	then
		Set the Shot Status of $currentShot to Valid
		Mark that evaluation of shot $currentShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Orthopoxvirus: If a shot is evaluated as Invalid due to being below the live virus interval, mark the shot Valid"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.860"
			- that has already been evaluated and whose Shot Validity Status is INVALID
			- make note of all evaluation reasons for this shot as $collectionOfStrReasons
			- the collection $collectionOfStrReasons contains "EVALUATION_REASON_CONCEPT.TOO_EARLY_LIVE_VIRUS"
			- the size of the collection $collectionOfStrReasons is == 1
			- make note of the Associated Series as $associatedTargetSeries
	then
		Set the shot status of $administeredShot to Valid
		Remove all evaluation reasons from shot $administeredShot
		Mark that evaluation of shot $administeredShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end


rule "Orthopoxvirus: If a shot is evaluated as Invalid due to being below the live virus interval and the series is an extra dose, mark the shot Accepted / EXTRA_DOSE"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.860"
			- that has already been evaluated and whose Shot Validity Status is INVALID
			- make note of invalid evaluation reasons for this shot as $collectionOfInvalidReasons
			- the size of the collection $collectionOfInvalidReasons is == 1
			- the collection $collectionOfInvalidReasons contains "EVALUATION_REASON_CONCEPT.TOO_EARLY_LIVE_VIRUS"
			- make note of accepted evaluation reasons for this shot as $collectionOfAcceptedReasons
			- the collection $collectionOfAcceptedReasons contains "EVALUATION_REASON_CONCEPT.EXTRA_DOSE"
			- make note of the Associated Series as $associatedTargetSeries
	then
		Set the shot status of $administeredShot to Accepted
		Remove evaluation reason "EVALUATION_REASON_CONCEPT.TOO_EARLY_LIVE_VIRUS" from shot $administeredShot
		Mark that evaluation of shot $administeredShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end
