/**
 * Copyright (C) 2024 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import java.util.Date
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.supportingdata.BaseDataRecommendationReason

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.cdsframework.ice.service.Schedule schedule


/*****************************************************************************************************************************************************************************************************************
===> PRIOR FORMULATION of COVID-19 Vaccine Administered on or after 9/12/2023   
 If the patient has a prior formulation COVID-19 shot that does not count towards U.S. vaccination (CVX 500, CVX 501, CVX 503, CVX 504, CVX 505, CVX 506, CVX 507, CVX 508, CVX 509, CVX 513, CVX 514, CVX 515, CVX 516, CVX 517, CVX 518, CVX 521) administered on or after 9/12/2023, the intervals to the next target dose are: 
     + Absolute Minimum Interval = 8 weeks - 4 days
     + Minimum Interval = 8 weeks
     + Recommended Interval = 8 weeks 
 /****************************************************************************************************************************************************************************************************************/

rule "COVID-19(Sep2023): If a prior formulation that does not count towards U.S. vaccination is administered (for any Sept 2023 COVID-19 Series), recommend earliest & recommended interval of 8 weeks to the next dose"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	/////// activation-group "recommendationIntervalCheck"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the Series is not Complete
		There is an administered shot $priorShot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE500", "ICE501", "ICE503", "ICE504", "ICE505", "ICE506", "ICE507", "ICE508", "ICE509", "ICE513", "ICE514", "ICE515", "ICE516", "ICE517", "ICE518", "ICE521")
			- make note of the Date this Shot was Administered as $previousShotDate
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $previousShotDate
			- the vaccine administered a member of ("ICE500", "ICE501", "ICE503", "ICE504", "ICE505", "ICE506", "ICE507", "ICE508", "ICE509", "ICE513", "ICE514", "ICE515", "ICE516", "ICE517", "ICE518", "ICE521")
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "8w" to $previousShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
 END ===> Series: ALL; Prior formulation administered in this season
*****************************************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 Recommendation Age Rules ===> All Series ; v1.42 release logic, still applicable in v1.44
*****************************************************************************************************************************************************************************************************************/

rule "COVID-19(Sep2023): If there are no shots administered, set the earliest/recommended/overdue age to the latter of age 6 months or 9/12/2023 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			/////// - the name of the Series is "COVID19Sep2023gte5Series"
			- the Series is not complete
			- the number of administered shots is == 0
		The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the date as $dtDateAtAge6m when the patient is "6m" of age
	then
		Create a Recommendation as $r for the Series $targetSeries
		Set the recommendation Earliest Forecast Date for $r to the latter of $dtDateAtAge6m and "12-Sep-2023"
		Set the recommendation Recommended Forecast Date for $r to the latter of $dtDateAtAge6m and "12-Sep-2023"
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023): If there are no shots administered, set the earliest age to the latter of age 6 months or 9/12/2023 (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023): If there are no shots administered, set the earliest/recommended/overdue age to the latter of age 6 months or 9/12/2023 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023): If there are no shots administered, set the latest recommended age to the latter of age 6 months or 9/12/2023 (*latestRecommendedAgeCheck*)"
		extends "COVID-19(Sep2023): If there are no shots administered, set the earliest/recommended/overdue age to the latter of age 6 months or 9/12/2023 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "latestRecommendedAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
 END Recommendation Age Rules ===> All Series
*****************************************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 ===> Series: >= 5 yrs
*****************************************************************************************************************************************************************************************************************/

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Recommendation Age / Interval Rules
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////
// "If the patient is >= 5 years of age as of the evaluation date, and the elapsed interval from the prior shot is < the date on which the patient turns age 5 years, the recommendation date the recommended interval after the last shot administered (and not at age 5 years)."
// NOTE / AI: This logic as specified on the wiki spe to follow series table interval (4 months), but this is dose 1; 8 weeks seems more correct
///////
 
rule "COVID-19(Sep2023 >= 5yrs): If the interval from the most recent shot administered >= 9/12/2023 has elapsed and the patient turns 5 years of age >= 9/12/2023, set the recommended age for target dose 1 to 9/12/2023 (*recommendationAgeCheck*)"
	// PREVIOUSLY, rule was: "...set earliest and recommended age for target dose 1 to 8w from the last administered shot (*recommendationAgeCheck*)", not to 9/12/2023
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		// Series Table Recommended Age will be overridden by this rule and not be enforced
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 1
			- the Series is not complete
		There is an administered shot $mostRecentShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- make note of the date this shot was administered as $mostRecentShotDate
        	- the date $mostRecentShotDate >= "12-Sep-2023"
			- the shot is not ignored
			- make note of the recommended interval from this dose to the next dose as $recommendedInterval
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $mostRecentShotDate
			- the shot is not ignored
		The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the date as $dtDateAtAge5y when the patient is "5y" of age
        	- the date $dtDateAtAge5y >= "12-Sep-2023"
        There exists an administered shot
        	- that is the same shot as $mostRecentShot
        	- the administration date of the shot is < $dtDateAtAge5y
        /////// Confirm elapsed time between $mostRecentShotDate and evalTime > "8w"   (previous interval used)
        Confirm elapsed time between $mostRecentShotDate and evalTime > $recommendedInterval
	then
		Create a Recommendation as $r for the Series $targetSeries
		// Set the earliest date here to avoid redundant calculation in child earliestAgeCheck rule (below)
		Set the recommendation Earliest Forecast Date for $r to "12-Sep-2023"
		Set the recommendation Recommended Forecast Date for $r to "12-Sep-2023"
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs): If 8 weeks from the last administered shot elapsed prior to the patient turning 5 years of age, set earliest and recommended age for target dose 1 to 9/12/2023 (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023 >= 5yrs): If the interval from the most recent shot administered >= 9/12/2023 has elapsed and the patient turns 5 years of age >= 9/12/2023, set the recommended age for target dose 1 to 9/12/2023 (*recommendationAgeCheck*)"
	// previously, rule was: "...set earliest and recommended age for target dose 1 to 8w from the last administered shot (*earliestAgeCheck*)", not to 9/12/2023
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
	when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

///////
// END If the interval from the most recent shot administered >= 9/12/2023 has elapsed and the patient turns 5 years of age >= 9/23/2023, set the recommended age for target dose 1 to 9/12/2023
///////


// rule "COVID-19(Sep2023 >= 5yrs): If there are no shots in the current season and only ignored shots in the prior season, set the earlest and recommended age date to 9/12/2023 (*recommendationAgeCheck*)"
rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1: If there are no shots in the current season but there are shots in the prior season, set the earlest and recommended date for target dose 1 to the latter of age 6 months or 9/12/2023 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 1
			- the Series is not complete
		There is an administered shot $mostRecentShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- make note of the date this shot was administered as $mostRecentShotDate
			- the administration date of the shot is < "12-Sep-2023"
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $mostRecentShotDate
		The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the date as $dtDateAtAge6m when the patient is "6m" of age
	then
		Create a Recommendation as $r for the Series $targetSeries
		Set the recommendation Earliest Forecast Date for $r to the latter of $dtDateAtAge6m and "12-Sep-2023"
		Set the recommendation Recommended Forecast Date for $r to the latter of $dtDateAtAge6m and "12-Sep-2023"
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs): If there are no shots in the current season but there are shots in the prior season, set the earlest and recommended age date to the latter of age 6 months or 9/12/2023 (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023 >= 5yrs)->TargetDose 1: If there are no shots in the current season but there are shots in the prior season, set the earlest and recommended date for target dose 1 to the latter of age 6 months or 9/12/2023 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
	when
		// Series Table Recommended Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// END Recommendation Age / Interval Rules
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Target Dose 1 Evaluation Logic
// 
// If the patient has >= 1 COVID-19 shot(s) on record prior to target dose 1, evaluate and recommend using the following intervals from the last shot, if none of the following exceptions apply: 
//     Absolute Minimum Interval = 8 weeks - 4 days
//     Minimum Interval = 8 weeks
//     Recommended Interval = 8 weeks
//
// Exceptions:
//     Exception 1: If a shot has been ignored (e.g., due to the CVX Code Absolute Maximum Age rule above or due to an inadvertent bivalent shot administered in the previous season), there is no absolute minimum interval 
//         from the ignored shot to the next target dose and no minimum interval or recommended interval from the ignored shot to the next target dose.
//     Exception 2: If a dose of CVX 313 is administered and it is the only dose on record, target dose 1 is not fulfilled, evaluate as Accepted with reason code VACCINE_NOT_PART_OF_THIS_SERIES.
//         Absolute minimum interval to fulfill target dose 1 = 24 days
//         Minimum interval = 28 days
//         Recommended interval = 28 days. If the patient is < 12 years as of the evaluation date and is < 12 years at the recommended due date), return recommended reason code ADMINISTER_mRNA_VACCINE.
//     Exception 3: If a dose of CVX 211 is administered this season (i.e., between 9/12/23 and 10/3/23), evaluate as Accepted with reason code VACCINE_NOT_PART_OF_THIS_SERIES.
//         Absolute minimum interval to fulfill target dose 1 = 24 days
//         Minimum interval = 28 days
//         Recommended interval = 28 days
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

///////
// START 
// TargetDose 1 Happy Path
// If the patient has >= 1 COVID-19 shot(s) on record prior to target dose 1, evaluate and recommend using the following intervals from the last shot, if none of the following exceptions apply: 
//     Minimum Interval = 8 weeks
//     Recommended Interval = 8 weeks
///////
rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Happy Path: If an unignored shot was administered (regardless of whether it is in the past season or present season) and the series is not complete, recommend earliest & recommended interval of 8w from target dose 1 to the next target dose"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationIntervalCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 1
			- the Series is not complete		
		There is an administered shot $mostRecentShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- make note of the date this shot was administered as $mostRecentShotDate
			- the shot is not ignored
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $mostRecentShotDate
			- the shot is not ignored
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "8w" to $mostRecentShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs): activation-group extension => if a shot was administered (regardless of whether it is in the past season or present season) but the series is not complete, recommend erarliest interval of 8w to the next target dose"
		extends "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Happy Path: If an unignored shot was administered (regardless of whether it is in the past season or present season) and the series is not complete, recommend earliest & recommended interval of 8w from target dose 1 to the next target dose"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestIntervalCheck"
	when
		// No condition required for this extension
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

////////
// END TargetDose 1 Happy Path
/////// 


///////
// START 
// Exception 1 
//     If a shot has been ignored (e.g., due to the CVX Code Absolute Maximum Age rule above or due to an inadvertent bivalent shot administered in the previous season), there is no absolute minimum interval from the ignored shot to the next target dose and no minimum interval 
//     or recommended interval from the ignored shot to the next target dose.
///////

// Nothing to do - ignored shots are automatically 0 day earliest & recommended interval per default rules


///////
// START
// Exception 2: If a dose of CVX 313 is administered and it is the only dose on record, target dose 1 is not fulfilled, evaluate as Accepted with reason code VACCINE_NOT_PART_OF_THIS_SERIES.
//     + Absolute minimum interval to fulfill target dose 1 = 24 days
//     + Minimum interval = 28 days
//     + Recommended interval = 28 days. If the patient is < 12 years as of the evaluation date and is < 12 years at the recommended due date), return recommended reason code ADMINISTER_mRNA_VACCINE.
///////

rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 2: If a shot of CVX 313 was previously administered for target dose 1 and evaluated as Accepted / VACCINE_NOT_ALLOWED_FOR_THIS_DOSE, and there are no prior doses on record, recommend earliest & recommended interval of 28 days from that shot to target dose 1"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationIntervalCheck"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 1
			- the Series is not complete
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- that has already been evaluated and whose shot validity is VALID
		There is an administered shot $shotOfInterest
			- the shot belongs to the Series $targetSeries
			- the dose number in the series is == 1
			- the vaccine administered is "ICE313"
			- that has already been evaluated and whose shot validity is ACCEPTED
			- make note of Accepted evaluation reasons for this shot as $acceptedReasons
			- the collection $acceptedReasons contains "EVALUATION_REASON_CONCEPT.VACCINE_NOT_ALLOWED_FOR_THIS_DOSE"
			- make note of the date this shot was administered as $shotOfInterestDate
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- the administration date of the shot is >= $shotOfInterestDate
			- the vaccine administered is not "ICE313"
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "28d" to $shotOfInterestDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 2: activation-group extension => If a shot of CVX 313 was previously administered for target dose 1 and evaluated as Accepted / VACCINE_NOT_ALLOWED_FOR_THIS_DOSE, and there are no prior doses on record, the earliest interval from that shot to target dose 1 is 28 days"
		extends "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 2: If a shot of CVX 313 was previously administered for target dose 1 and evaluated as Accepted / VACCINE_NOT_ALLOWED_FOR_THIS_DOSE, and there are no prior doses on record, recommend earliest & recommended interval of 28 days from that shot to target dose 1"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestIntervalCheck"
	salience 10
	when
		// No condition required for this extension
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 2: If the patient is < 12 years as of the evaluation date and is < 12 years at the recommended due date), CVX 313 was administered for target dose 1 and it is Invalid, return recommended reason code ADMINISTER_mRNA_VACCINE."
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck^COVID19Sep2023Season"
	when
		There is a Series $targetSeries
			- a forecast for the Series has been made and a recommendation date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 1
			- make note of the recommendation date as $recommendationDate
			- post processing on the Series forecast has not already been run
		There is an administered shot $previousShot
			- the shot belongs to the Series $targetSeries
			- the dose number in the series is == 1
			- the vaccine administered is "ICE313"
			- that has already been evaluated and whose shot validity is ACCEPTED
			- make note of Accepted evaluation reasons for this shot as $acceptedReasons
			- the collection $acceptedReasons contains "EVALUATION_REASON_CONCEPT.VACCINE_NOT_ALLOWED_FOR_THIS_DOSE" 
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and evalTime < "12y" && elapsed time between $birthDate and $recommendationDate < "12y"
	then
		Create a Recommendation as $recommendation for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.ADMINISTER_mRNA_VACCINE"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

////////
// END TargetDose 1 Exception 2
/////// 


///////
// TargetDose 1 / Exception 3: If a dose of CVX 211 is administered this season (i.e., between 9/12/23 and 10/3/23), evaluate as Accepted with reason code VACCINE_NOT_PART_OF_THIS_SERIES.
//    Absolute minimum interval to fulfill target dose 1 = 24 days
//    Minimum interval = 28 days
//    Recommended interval = 28 days
///////

rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 3: If a shot of CVX 211 was previously administered for target dose 1 and evaluated as Accepted / VACCINE_NOT_PART_OF_THIS_SERIES, the recommended interval from that shot to target dose 1 is 28 days"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the number of doses administered is == 0
			- the Series is not complete
		There is an administered shot $shotOfInterest
			- the shot belongs to the Series $targetSeries
			- the dose number in the series is == 1
			- the vaccine administered is "ICE211"
			- that has already been evaluated and whose shot validity is ACCEPTED
			- make note of the date this shot was administered as $cvx211administrationDate
			- make note of Accepted evaluation reasons for this shot as $acceptedReasons
			- the collection $acceptedReasons contains "EVALUATION_REASON_CONCEPT.VACCINE_NOT_PART_OF_THIS_SERIES" 
			- make note of the date this shot was administered as $previousShotDate
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "28d" to $previousShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// We include override rules here because the new interval, 28 days, is less than the default interval of 4 months in the Series (interval from dose 1 to dose 2 in the COVID19Sep2023gte5Series series). 
// Typically, we never wrote rules like this because we always wanted to longest recommended interval to apply. In this case, we do not want the series table's interval to apply.
rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 3: There are no other shots administered after the CVX 211; therefore, _also_ override default recommendationIntervalCheck"
		extends "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 3: If a shot of CVX 211 was previously administered for target dose 1 and evaluated as Accepted / VACCINE_NOT_PART_OF_THIS_SERIES, the recommended interval from that shot to target dose 1 is 28 days"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationIntervalCheck"
	when
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- the administration date of the shot is >= $cvx211administrationDate
			- the vaccine administered is not "ICE211"
	then
		// Nothing to do; simply overriding default doseIntervalRule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 3: There are no other shots administered after the CVX 211; therefore, _also_ override default earliestIntervalCheck"
		extends "COVID-19(Sep2023 >= 5yrs)->TargetDose 1/Exception 3: There are no other shots administered after the CVX 211; therefore, _also_ override default recommendationIntervalCheck"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestIntervalCheck"
	when
		// No additional checks needed
	then
		// Nothing to do; simply overriding default doseIntervalRule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

////////
// END TargetDose 1 Exception 3
///////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// TargetDose 2: the recommended date is the latter of the calculated forecast date or February 28, 2024.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023): The recommended age of dose 2 is the latter of age 65 years or 2/28/2024 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023gte5Series"
			- the effective dose number in the series is == 2
			- the Series is not complete
		The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the date as $dtDateAtAge65y when the patient is "65y" of age
	then
		Create a Recommendation as $r for the Series $targetSeries
		Set the recommendation Earliest Forecast Date for $r to the latter of $dtDateAtAge65y and "28-Feb-2024"
		Set the recommendation Recommended Forecast Date for $r to the latter of $dtDateAtAge65y and "28-Feb-2024"
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023): If there are no shots administered, set the earliest age to the latter of age 65 years or 2/28/2023 (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023): The recommended age of dose 2 is the latter of age 65 years or 2/28/2024 (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// END TargetDose 2: the recommended date is the latter of the calculated forecast date or February 28, 2024.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/*****************************************************************************************************************************************************************************************************************
 END Series: >= 5 yrs: Recommendation Interval / Interval Rules
*****************************************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 ===> Series: Pfizer < 5 yrs
*****************************************************************************************************************************************************************************************************************/

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ===> Pfizer < 5 yrs Series  
// If a shot is administered below the series absolute minimum age (< 6m - 4 days) in this season (on or after 9/12/2023):
// If administered as target dose 1 or target dose 2, intervals to the next target dose are:  
//    Absolute minimum interval = 24 days
//    Minimum interval = 28 days
//    Recommended interval = 28 days 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Pfizer < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023PfizerLT5ySeries"
			- the Series is not complete
			- the effective number of doses administered in the Series is <= 2
		There is an administered shot $mostRecentShot
			- the shot belongs to the series $targetSeries
			- make note of the date this shot was administered as $mostRecentShotDate
			- the shot is not ignored
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $mostRecentShotDate
			- the shot is not ignored
		There exists an IceFact
			- that has associated administered shot $mostRecentShot
			- that has finding SupportedFactConcept._BELOW_MINIMUM_AGE.conceptCodeValue 
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "28d" to $mostRecentShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

rule "COVID-19(Sep2023 Pfizer < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023 Pfizer < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
 END Series: Pfizer < 5 yrs
*****************************************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 ===> Series: Moderna < 5 yrs
*****************************************************************************************************************************************************************************************************************/

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ===> Moderna < 5 yrs Series
// If a shot is administered below the series absolute minimum age (< 6m - 4 days) in this season (on or after 9/12/2023) , intervals to the next target dose are:  
//    Absolute minimum interval = 24 days
//    Minimum interval = 28 days
//    Recommended interval = 28 days 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Moderna < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023ModernaLT5ySeries"
			- the Series is not complete
		There is an administered shot $mostRecentShot
			- the shot belongs to the series $targetSeries
			- make note of the date this shot was administered as $mostRecentShotDate
			- the shot is not ignored
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $mostRecentShotDate
			- the shot is not ignored
		There exists an IceFact
			- that has associated administered shot $mostRecentShot
			- that has finding SupportedFactConcept._BELOW_MINIMUM_AGE.conceptCodeValue 
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "28d" to $mostRecentShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

rule "COVID-19(Sep2023 Moderna < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023 Moderna < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
 END Series: Moderna < 5 yrs
*****************************************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 ===> Series: Mixed Product < 5 yrs
*****************************************************************************************************************************************************************************************************************/

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// ===> Mixed Product < 5 yrs Series
// If a shot is administered below the series absolute minimum age (< 6m - 4 days) in this season (on or after 9/12/2023):  
//    If administered as target dose 1 or target dose 2, intervals to the next target dose are:  
//        Absolute minimum interval = 24 days
//        Minimum interval = 28 days
//        Recommended interval = 28 days 
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Mixed Product < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023MixedProductLT5ySeries"
			- the Series is not complete
			- the effective number of doses administered in the Series is <= 2
		There is an administered shot $mostRecentShot
			- the shot belongs to the series $targetSeries
			- make note of the date this shot was administered as $mostRecentShotDate
			- the shot is not ignored
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $mostRecentShotDate
			- the shot is not ignored
		There exists an IceFact
			- that has associated administered shot $mostRecentShot
			- that has finding SupportedFactConcept._BELOW_MINIMUM_AGE.conceptCodeValue 
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "28d" to $mostRecentShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

rule "COVID-19(Sep2023 Mixed Product < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*earliestAgeCheck*)"
		extends "COVID-19(Sep2023 Mixed Product < 5yrs Series): If a shot was previously administered for target dose 1 or target dose 2 and evaluated as Invalid / BELOW_MINIMUM_AGE_SERIES and the series is not complete, the minimum/recommended interval from that shot to the next shot is 28 days (*recommendationAgeCheck*)"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
when
		// Series Table Earliest Age will be overridden by this rule and will not be enforced
	then
		// Nothing to do; earliest forecast date set in parent rule
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// If the most recent dose administered was a Novavax (CVX 313) to a patient age >= 6 months - 4 days and < 5 years and the patient is not complete for a Sept 2023 COVID-19 series, return recommended reason code ADMINISTER_mRNA_VACCINE.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Mixed Product < 5yrs Series): If the most recent shot administered was a Novavax (CVX 313) to a patient age >= 6m-4d and < 5 yrs and the series is not complete, return recommended reason code ADMINISTER_mRNA_VACCINE"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023MixedProductLT5ySeries"
			- the Series is not complete
			- make note of the last shot administered in the Series as $lastShotAdministered
		There is an administered shot $sameShot
			- that is the same shot as $lastShotAdministered
			- the vaccine administered is "ICE313"
			- make note of the minimum vaccine age for this shot as $absMinVaccineAge
			- make note of the date this shot was administered as $shotAdministrationDate
		The patient information $patient must be known to complete writing this rule
			- make note of the patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and $shotAdministrationDate >= $absMinVaccineAge
		Confirm elapsed time between $birthDate and $shotAdministrationDate < "5y"
	then
		Create a Recommendation as $recommendation for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.ADMINISTER_mRNA_VACCINE"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
END Series: Mixed Product < 5 yrs
*****************************************************************************************************************************************************************************************************************/




/*************************************************************************************************************************************************************************************
 ===> Series: Sep2023 Novavax
/************************************************************************************************************************************************************************************/

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// If the series has not been completed but the number of doses to complete the Novavax series has been met, then the series must
// have been completed with all prior CVX 211 vaccine. The minimum/recommended interval to the next dose (which should be a CVX 311) is 8w 
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Novavax Series): If a shot was administered for any dose number and the series is not complete, recommend earliest and recommended interval of 8w to the next target dose"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023NovavaxSeries"
			- the Series is not complete
			- the number of administered shots is >= 1
			- make note of the number of doses required to complete this Series as $numberOfDosesInSeries
			- the effective number of doses administered in the Series is >= $numberOfDosesInSeries
			- make note of the last shot administered in the Series as $lastShotInSeries
			- make note of the date that the most recent shot was administered as $lastShotDateInSeries
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "8w" to $lastShotDateInSeries and make note of the newly calculated date as $dtCalculatedMinimum
		Add "8w" to $lastShotDateInSeries and make note of the newly calculated date as $dtCalculatedRecommended
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// If the patient is age >= 5 years and < 12 years and CVX 313 is administered as dose 1, evaluate and recommend using the following intervals:  
//     Minimum Interval = 4 weeks
//     Recommended Interval = 4 weeks. 
// Furthermore, if the patient is < 12 years as of the evaluation date and is < 12 years at the recommended due date), return recommended reason code ADMINISTER_mRNA_VACCINE.
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Novavax Series): If a CVX 313 is administered as (valid) dose 1 to a patient >= 5 yrs and < 12 yrs-4 days, recommend earliest/recommended interval of 8 weeks from dose 1"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023NovavaxSeries"
			- the number of administered shots is >= 1
			- the effective number of doses administered in the Series is == 1
			- Make note of the last shot administered in the Series as $lastShotAdministered
		There is an administered shot $CVX313Dose1
			- the shot belongs to the Series $targetSeries
			- the vaccine administered is "ICE313"
			- that has already been evaluated and whose shot validity is VALID
			- the dose number in the series is == 1
		There is an administered shot $mostRecentShot
			- that is the same shot as $lastShotAdministered
			- make note of the date this shot was administered as $lastShotAdministrationDate
		The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the patient's birthdate as $dtBirthDate
        Confirm elapsed time between $dtBirthDate and $lastShotAdministrationDate >= "5y"
        Confirm elapsed time between $dtBirthDate and $lastShotAdministrationDate < "12y-4d"
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "4w" to $lastShotAdministrationDate and make note of the newly calculated date as $dtCalculatedMinimum
		Add "4w" to $lastShotAdministrationDate and make note of the newly calculated date as $dtCalculatedRecommended
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 Novavax Series):activation-group extension => If a CVX 313 is administered as (valid) dose 1 to a patient >= 5 yrs and < 12 yrs, recommend earliest/recommended interval of 8 weeks from dose 1"
		extends "COVID-19(Sep2023 Novavax Series): If a CVX 313 is administered as (valid) dose 1 to a patient >= 5 yrs and < 12 yrs-4 days, recommend earliest/recommended interval of 8 weeks from dose 1"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "earliestAgeCheck"
	when
		// No condition required for this extension
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 Novavax Series): If the patient is < 12 years as of the evaluation date and is < 12 years at the recommended due date), return recommended reason code ADMINISTER_mRNA_VACCINE."
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck^COVID19Sep2023Season"
	when
		There is a Series $targetSeries
			- a forecast for the Series has been made and a recommendation date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023NovavaxSeries"
			- make note of the recommendation date as $recommendationDate
			- post processing on the Series forecast has not already been run
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and evalTime < "12y" && elapsed time between $birthDate and $recommendationDate < "12y"
	then
		Create a Recommendation as $recommendation for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.ADMINISTER_mRNA_VACCINE"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end	


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// For target dose 4, the recommended date is the later of the calculated forecast date or February 28, 2024.
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Sep2023 Novavax Series): For target dose 4, the earliest & recommended date is the latter of the calculated forecast date or 2/28/2024"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023NovavaxSeries"
			- the effective dose number in the Series is == 4
			The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the date as $dtDateAtAge65y when the patient is "65y" of age
	then
		Create a Recommendation as $r for the Series $targetSeries
		Set the recommendation Earliest Forecast Date for $r to the latter of $dtDateAtAge65y and "28-Feb-2024"
		Set the recommendation Recommended Forecast Date for $r to the latter of $dtDateAtAge65y and "28-Feb-2024"
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END ===> Series: Sep2023 Novavax
/************************************************************************************************************************************************************************************/




/*****************************************************************************************************************************************************************************************************************
 ===> Intervals from PRIOR Season ; v1.42 release logic, still applicable
 ===> Series: >= 5 yrs, Pfizer < 5 yrs, Moderna < 5 yrs or Mixed Product < 5 yrs
   If a prior formulation is administered for target dose 1, recommend interval of 28 days to the next dose  (See above)
 Otherwise:
 If the patient has >= 1 COVID-19 shot(s) on record prior to (a valid) dose 1, recommend using the following intervals from the last shot in the  
   >= 5 yrs, Pfizer < 5 yrs, Moderna < 5 yrs or Mixed Product < 5 yrs series.
     + Minimum Interval = 8 weeks
     + Recommended Interval = 8 weeks
*****************************************************************************************************************************************************************************************************************/

rule "COVID-19(Sep2023 Pfizer/Moderna/Mixed Product < 5yrs): For target dose 1 of the current series' season: shot(s) have been administered in the current season and there are also shots in the prior season but none of them are (valid) doses. Recommend earliest/recommended/overdue interval of 8w from the most recent shot in the previous season"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19Sep2023PfizerLT5ySeries", "COVID19Sep2023ModernaLT5ySeries", "COVID19Sep2023MixedProductLT5ySeries")
			- the Series is not complete
			- the effective dose number in the Series is == 1
		There does not exist an administered shot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
		There is an administered shot $previousShot
			- the administration date of the shot is < "12-Sep-2023"
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot is not ignored
			- make note of the date this shot was administered as $previousShotDate
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is > $previousShotDate
			- the administration date of the shot is < "12-Sep-2023"
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "8w" to $previousShotDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Set the recommendation Overdue Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 Pfizer/Moderna/Mixed Product < 5yrs Series): For target dose 1 of the current series' season: no shots have been administered in the current season but there have been shots administered in the prior season. Enforce the earliest/recommended recommendation interval for the current series dose from the most recent shot in the prior season"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19Sep2023PfizerLT5ySeries", "COVID19Sep2023MixedProductLT5ySeries", "COVID19Sep2023ModernaLT5ySeries")
			- the Series is not complete
			- the number of administered shots is == 0
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is == 1
		There is an administered shot $mostRecentShotPreviousSeason
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- make note of the date this shot was administered as $mostRecentShotDate
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $mostRecentShotDate
	then
		Obtain the existing DoseRule for Dose Number 1 in the Series $targetSeries as $oDoseRule
		Obtain the Minimum Interval from the existing DoseRule $oDoseRule as $oMinimumInterval
		Obtain the Recommended Interval from the existing DoseRule $oDoseRule as $oRecommendedInterval
		Create a Recommendation as $r for the Series $targetSeries
		Add $oMinimumInterval to $mostRecentShotDate and make note of the newly calculated date as $dtCalculatedMinimum
		Add $oRecommendedInterval to $mostRecentShotDate and make note of the newly calculated date as $dtCalculatedRecommended
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 Pfizer/Moderna/Mixed Product < 5yrs Series): For target dose > 1: If no shots have been administered in the current season. Enforce the earliest/recommended recommendation interval for the current series dose from the most recent shot in the previous season"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season^CustomIntervals"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19Sep2023PfizerLT5ySeries", "COVID19Sep2023MixedProductLT5ySeries", "COVID19Sep2023ModernaLT5ySeries")
			- the Series is not complete
			- the number of administered shots is == 0
			- make note of the number of doses required to complete this Series as $numberOfDosesInSeries
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is > 1
			- the numeric $effectiveDoseNumber is <= $numberOfDosesInSeries
		There is an administered shot $mostRecentShotPreviousSeason
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- make note of the date this shot was administered as $mostRecentShotDate
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $mostRecentShotDate
	then
		Obtain the existing DoseRule for Dose Number $effectiveDoseNumber-1 in the Series $targetSeries as $oDoseRule
		Obtain the Minimum Interval from the existing DoseRule $oDoseRule as $oMinimumInterval
		Obtain the Recommended Interval from the existing DoseRule $oDoseRule as $oRecommendedInterval
		/////// Obtain the Latest Recommended Interval from the existing DoseRule $oDoseRule as $oLatestRecommendedInterval
		Create a Recommendation as $r for the Series $targetSeries
		Add $oMinimumInterval to $mostRecentShotDate and make note of the newly calculated date as $dtCalculatedMinimum
		Add $oRecommendedInterval to $mostRecentShotDate and make note of the newly calculated date as $dtCalculatedRecommended
		/////// Add $oLatestRecommendedInterval to $mostRecentShotDate and make note of the newly calculated date as $dtCalculatedRecommendedLatest
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		/////// Set the recommendation Overdue Forecast Date for $r to $dtCalculatedRecommendedLatest
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/////////////////////
// If the patient has >= 2 doses of any of the following prior Moderna COVID-19 vaccines (CVX 207, CVX 221, CVX 227, CVX 228, CVX 229, CVX 230, CVX 519) administered before 9/12/2023 
// and there does not exist a shot administered prior to 9/12/2023 at >= 5 years of age, target dose 1 is not needed. Skip to target dose 2. 
//     + Absolute minimum interval = 8 weeks - 4 days
//     + Minimum interval = 8 weeks 
//     + Recommended interval = 8 weeks 
/////////////////////

rule "COVID-19(Sep2023 __Moderna__ < 5yrs Series): For target dose > 1: If 2 or more doses were administered in the prior season, earliest/recommended interval to the next dose is 8 weeks"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	activation-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season^CustomIntervals"
	no-loop true
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023ModernaLT5ySeries"
			- the Series is not complete
			// - at least one shot has been administered
			- the number of doses administered is == 0
			- the effective dose number in the Series is > 1
		// Obtain the most recent dose in the prior season
		There is an administered shot $previousShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
			- make note of the date this shot was administered as $mostRecentShotDatePriorSeason
			- make note of the associated series as $priorSeasonSeries
		// We want to run this rule for one and only one dose from the prior season; therefore, make sure we're looking at the most recent dose in that season
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the series $priorSeasonSeries
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $mostRecentShotDatePriorSeason
			- that has already been evaluated and whose shot validity is VALID
		// Now check to make sure there is a different dose in the prior season in the same series
		There exists an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- that is not the same shot as $previousShot
			- the shot belongs to the series $priorSeasonSeries
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is != $mostRecentShotDatePriorSeason
			- that has already been evaluated and whose shot validity is VALID
	then
		// Create a recommendation of 8 weeks from the prior season's most recent shot
		Create a Recommendation as $r for the Series $targetSeries
		Add "8w" to $mostRecentShotDatePriorSeason and make note of the newly calculated date as $dtCalculatedMinimum
		Add "8w" to $mostRecentShotDatePriorSeason and make note of the newly calculated date as $dtCalculatedRecommended
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		Include the Recommendation $r for Consideration in the final forecast of the Series
		// Now update the DoseRule for dose number 1, in case there are shots in the current season too
		Obtain the existing DoseRule for Dose Number 1 in the Series $targetSeries as $oDoseRule
		Obtain the Minimum Interval from the existing DoseRule $oDoseRule as $oMinimumInterval
		Obtain the Recommended Interval from the existing DoseRule $oDoseRule as $oRecommendedInterval
		Set the Minimum Interval for DoseRule $oDoseRule to TimePeriod "8w"
		Set the Earliest Recommended Interval for DoseRule $oDoseRule to TimePeriod "8w"
		Replace the existing DoseRule in the Series $targetSeries with $oDoseRule
		Refresh all facts in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*****************************************************************************************************************************************************************************************************************
 END Recommendation Interval Rules from prior season; Series: >= 5 yrs, Pfizer < 5 yrs, Moderna < 5 yrs or Mixed Product < 5 yrs ; v1.42 release logic, still applicable
*****************************************************************************************************************************************************************************************************************/




/*************************************************************************************************************************************************************************************
 ===> Series: >= 5 yrs, Pfizer < 5 yrs, Moderna < 5 yrs or Mixed Product < 5 yrs ; v1.42 release logic, still applicable
 __SKIP DOSE__ Rules FOR < 5 yrs of age series: Pfizer < 5ys, Moderna < 5yrs, Mixed Product < 5yrs; Novavax
     + Target Dose 1
         If the patient has 1 dose of any of the following prior formulations vaccines (CVX 207, CVX 208, CVX 217, CVX 218, CVX 219, CVX 221, CVX 227, CVX 228, CVX 229, CVX 230, 
         CVX 300, CVX 301, CVX 302, or CVX 520) administered prior to 9/12/2023, target dose 1 is not needed. Skip to target dose 2. 
     + Target Dose 1 and Target Dose 2
         If the patient has >= 2 doses of any of the following following prior formulations vaccines (CVX 207, CVX 208, CVX 217, CVX 218, CVX 219, CVX 221, CVX 227, CVX 228, CVX 229, 
         CVX 230, CVX 300, CVX 301, CVX 302, or CVX 520) administered prior to 9/12/2023, target dose 1 and target dose 2 are not needed. Skip to target dose 3. 
     
===> Series: Novavax
     + If CVX 313 was administered for dose 1 or dose 2, dose 3 is not needed. Skip to target dose 4. 
/************************************************************************************************************************************************************************************/

rule "COVID-19(Sep2023 __Pfizer/Mixed Product__ < 5yrs Series): if the patient has 1 dose of a prior formulation administered prior to 9/12/2023 at < 5yrs of age, set the recommendation dose number to 2"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	salience 999
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19Sep2023PfizerLT5ySeries", "COVID19Sep2023MixedProductLT5ySeries")
			- the Series is not complete
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is <= 2
			- the dose number to recommend is < 2
		// Obtain the most recent dose in the prior season
		There is an administered shot $previousShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedPriorSeasonSeries
		// We want to run this rule for one and only one dose from the prior season; therefore, make sure we're looking at the most recent dose in that season
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $administrationDate
			- that has already been evaluated and whose shot validity is VALID
		// Now check to make sure there is not a different dose in the prior season
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- that is not the same shot as $previousShot
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is != $administrationDate
			- that has already been evaluated and whose shot validity is VALID
	then
		Skip Series Dose Number to 2 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.079.89" in Series $targetSeries but do not refresh series facts
		Set the Dose Number of Recommendation Forecast to 2 in Series $targetSeries
		Refresh all facts in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 __Pfizer/Mixed Product__ < 5yrs Series): if the patient has >= 2 doses of a prior formulation administered prior to 9/12/2023 at < 5yrs of age, set the recommendation dose number to 3"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	salience 999
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19Sep2023PfizerLT5ySeries", "COVID19Sep2023MixedProductLT5ySeries")
			- the Series is not complete
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is <= 3
			- the dose number to recommend is < 3
		// Obtain the most recent dose in the prior season
		There is an administered shot $previousShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $priorSeasonSeries
		// We want to run this rule for one and only one dose from the prior season; therefore, make sure we're looking at the most recent dose in that season
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the series $priorSeasonSeries
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $administrationDate
			- that has already been evaluated and whose shot validity is VALID
		// Now check to make sure there is a different dose in the prior season in the same series
		There exists an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- that is not the same shot as $previousShot
			- the shot belongs to the series $priorSeasonSeries
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
	then
		Skip Series Dose Number to 3 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.079.89" in Series $targetSeries but do not refresh series facts
		Set the Dose Number of Recommendation Forecast to 3 in Series $targetSeries
		Refresh all facts in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 __Moderna__ < 5yrs Series): if the patient has >= 1 doses of a prior formulation administered prior to 9/12/2023 at < 5yrs of age, set the recommendation dose number to 2"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	salience 999
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023ModernaLT5ySeries"
			- the Series is not complete
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is <= 2
			- the dose number to recommend is < 2
		// Obtain the most recent dose in the prior season
		There is an administered shot $previousShot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administration date of the shot is < "12-Sep-2023"
			- that has already been evaluated and whose shot validity is VALID
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $priorSeasonSeries
		// We want to run this rule for one and only one dose from the prior season; therefore, make sure we're looking at the most recent dose in that season
		There does not exist an administered shot
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the series $priorSeasonSeries
			- the administration date of the shot is < "12-Sep-2023"
			- the administration date of the shot is > $administrationDate
			- that has already been evaluated and whose shot validity is VALID
	then
		Skip Series Dose Number to 2 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.079.89" in Series $targetSeries but do not refresh series facts
		Set the Dose Number of Recommendation Forecast to 2 in Series $targetSeries
		Refresh all facts in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "COVID-19(Sep2023 __Novavax__ Series): If CVX 313 was administered for dose 1 or dose 2, dose 3 is not needed. Skip to target dose 4"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule^COVID19Sep2023Season"
	salience 999
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series is "COVID19Sep2023NovavaxSeries"
			- make note of the effective dose number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is == 3			
		There is an administered shot $cvx313dose1or2
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose shot validity is VALID
			- the vaccine administered is "ICE313"
			- the dose number in the Series is <= 2
	then
		Skip Series Dose Number to 4 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.079.89" in Series $targetSeries but do not refresh series facts
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Refresh all facts in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END __SKIP DOSE__ Logic for < 5 yrs Series  (Pfizer, Moderna, Mixed Product, Novavax)
/************************************************************************************************************************************************************************************/
