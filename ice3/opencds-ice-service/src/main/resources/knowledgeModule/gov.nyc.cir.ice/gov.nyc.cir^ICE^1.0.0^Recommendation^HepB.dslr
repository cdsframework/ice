/**
 * Copyright (C) 2023 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.Date
import org.opencds.vmr.v1_0.internal.datatypes.CD
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType

global java.util.Date evalTime
global org.cdsframework.ice.service.Schedule schedule

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl
 

// Overrides routine recommendationAgeCheck
rule "HepB: Recommend a Dose of HepB Conditionally if Patient >= 60 Years and No Previously Administered Doses"
	agenda-group "RecommendationForecast^customRecommendationRule"
	activation-group "recommendationAgeCheck"
    when
        There is a Series $targetSeries that needs forecasting
        	- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.100"
        	- the number of Administered Shots is == 0
        The patient information $oEvaluatedPerson must be known to complete writing this rule
        	- make note of the patient's birthdate as $dtBirthDate
        Confirm elapsed time between $dtBirthDate and evalTime >= 60 DurationType.YEARS
    then
        Add "60y" to $dtBirthDate and make note of the newly calculated date as $dtCalculated
		Include a Recommendation as $r with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED and Recommended Forecast Date $dtCalculated for Consideration in the final Forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

// The interval between dose 1 & 3 of HepB needs to be at least 112 days for Recommendation. Routine interval rule still runs
rule "HepB: Recommend both an earliest interval and recommended interval of 112 days between doses 1 & 3 for Child/Adolescent Series"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.100"
			- the name of the series is "HepBChildAdolescentSeries"
			- the Series is not Complete
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "112d" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Set the recommendation Earliest Forecast Date for $r to $dtCalculated
		Set the recommendation Recommended Forecast Date for $r to $dtCalculated
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// The interval between dose 1 & 3 of HepB needs to be at least 112 days for Recommendation. Routine interval rule still runs
rule "HepB: Recommend both an earliest interval of 112 days and recommended interval of 6 months between doses 1 & 3 for Adult 3-dose Series"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.100"
			- the name of the series is "HepBAdult3DoseSeries"
			- the Series is not Complete
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate
	then
		Create a Recommendation as $r for the Series $targetSeries
		Add "112d" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculatedMinimum
		Add "6m" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculatedRecommended
		Set the recommendation Earliest Forecast Date for $r to $dtCalculatedMinimum
		Set the recommendation Recommended Forecast Date for $r to $dtCalculatedRecommended
		Include the Recommendation $r for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Recommend at the vaccine level, CVX 189 (Hep B adjuvanted) vaccine, when recommending for the Hep B 2-dose Adult Series
rule "Hep B: Recommend CVX 189 (Hep B adjuvanted) for the Hep B 2-dose Adult Series"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.100"
			- the Name of the Series is "HepBAdult2DoseSeries"
			- the Series is Not Complete
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE189"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end
   
