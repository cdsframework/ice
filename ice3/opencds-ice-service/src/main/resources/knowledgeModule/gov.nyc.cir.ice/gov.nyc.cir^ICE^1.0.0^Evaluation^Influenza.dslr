/**
 * Copyright (C) 2023 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.drools.core.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime


rule "Influenza: 24 day absolute minimum interval between target dose 1 in current seasonal series and most recent shot from a prior season"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheckFromPreviousSeason"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
			- the dose number in the series is == 1
			- make note of the Associated Series as $associatedTargetSeries
			- make note of the date this shot was administered as $dtAdministrationDate
		There is a series $targetSeries identified by $associatedTargetSeries
			- the series is a seasonal series
			- make note of the season start date as $dtSeasonStartDate
		There is an administered shot $priorAdministeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
			- the shot does not belong to the series $associatedTargetSeries
			- that has already been evaluated
			- the administration date of the shot is < $dtSeasonStartDate
			- make note of the date this shot was administered as $priorAdministrationDate
		There does not exist an administered shot
			 - the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
			 - the shot does not belong to the series $associatedTargetSeries
			 - that has already been evaluated
			 - the administration date of the shot is > $priorAdministrationDate
		Confirm Elapsed time between $priorAdministrationDate and $dtAdministrationDate < "24d"
	then
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "Influenza: Evaluate the Influenza Shot as Outside Flu Season if it does not fall within the Season Start and Stop Dates"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseInSeasonCheck"
	when  
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.800"
			- the Shot $currentShot does not fall within the Season start and stop dates of the Series
	then
		Include the reason for shot $currentShot Invalid due to "Outside Flu Season"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries 
end 


rule "Influenza: Change invalid >= maximum age reason to Insufficient Antigen for vaccine CVX 161 (injectable quadrivalent, preservative-free, pediatric) is Administered"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "maximumAgeVaccineCheck"
    when
    	There is an Administered Shot $oTargetDose that needs to be evaluated
    		- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
       		- the vaccine administered is "ICE161"
       		- the vaccine administered has a maximum valid age
       		- make note of the Associated Series as $associatedTargetSeries
    	The patient information $oEvaluatedPerson must be known to complete writing this rule
		Confirm the age of the patient $oEvaluatedPerson at the time the vaccine was administered for dose $oTargetDose is greater than the maximum allowable age for the vaccine
    then
        Include the reason for shot $oTargetDose Invalid due to "Insufficient Antigen"
        Record that this dose rule was Processed for the TargetDose $oTargetDose
        Log that this dose rule fired for the dose $oTargetDose in the Series $associatedTargetSeries
end


rule "Influenza: If CVX 194, 200, 201 202, or 231 is administered, evaluate the shot as Invalid / VACCINE_NOT_ALLOWED_IN_US"
	agenda-group "HistoryEvaluation^customEvaluationRule"
    when
    	There is an Administered Shot $oTargetDose that needs to be evaluated
    		- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.800"
    		- the vaccine administered a member of ("ICE194", "ICE200", "ICE201", "ICE202", "ICE231")
       		- make note of the Associated Series as $associatedTargetSeries
    then
    	Include the reason for shot $oTargetDose Invalid due to "Vaccine not Allowed in U.S."
        Record that this dose rule was Processed for the TargetDose $oTargetDose
        Log that this dose rule fired for the dose $oTargetDose in the Series $associatedTargetSeries
end
