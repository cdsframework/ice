/**
 * Copyright (C) 2023 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.Date
import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime


//////////////
// START - Bypass vaccine age checks if the primary series is complete
//////////////
rule "DTP: Bypass absolute minimum age for vaccine check if the primary series is complete"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "DTP: Bypass absolute maximum age for vaccine check if the primary series is complete"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "maximumAgeVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

//////////////
// END - Bypass vaccine age checks if the primary series is complete
//////////////

  
/*************************************************************************************************************************************************************************************
Rules for Recurring Td (CVX 09)
Once the patient has completed the primary DTP series (3-dose or 5-dose) and has received a dose of pertussis at >= 7 years of age, a Td (09) is needed every 10 years.  

Evaluation Rules for Recurring Td
    + All vaccines in the DTP vaccine group are allowed for the recurring Td.
    + Minimum interval = 0 days 
    + Once the primary series is complete and the patient has a dose of pertussis at >=7, any DTP shot is evaluated as VALID for the recurring Td.
*************************************************************************************************************************************************************************************/

// Rules for Recurring Td (CVX 09) 
// Once the patient has completed the primary DTP series (3-dose or 5-dose) and has received a dose of pertussis at >= 7 years of age, a Td (09) is needed every 10 years.
// All vaccines in the DTP vaccine group are allowed for the recurring Td.
// Once the primary series is complete and the patient has a dose of pertussis at >=7, any DTP shot is evaluated as VALID for the recurring Td.  
rule "DTP: Evaluate recurring Td as valid if Td (age) evaluation criteria is met"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"	// Prevent default series interval rule from firing, which would mark this shop as ACCEPTED/EXTRA_DOSE instead of simply valid.
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_COMPLETED.conceptCodeValue
			- that has Associated Series $targetSeries
	then
		Include the reason for shot $currentShot Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// Interval check for recurring Td. Minimum interval = 0 days 
rule "DTP: Evaluate recurring Td as valid if Td (interval) evaluation criteria is met" extends "DTP: Evaluate recurring Td as valid if Td (age) evaluation criteria is met"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck" // This activation-group prevents the default series interval rule from firing, which would mark this shop as ACCEPTED/EXTRA_DOSE instead of simply valid.
	when
		// Since the absolute minimum interval is 0 days, no actual interval check performed.
	then
		Include the reason for shot $currentShot Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// Interval check for recurring Td. Minimum interval = 0 days 
rule "DTP: Override default series rule that evaluates extra Td shots in the series as accepted" extends "DTP: Evaluate recurring Td as valid if Td (age) evaluation criteria is met"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck" // This activation-group prevents the default series interval rule from firing, which would mark this shop as ACCEPTED/EXTRA_DOSE instead of simply valid.
	when
		// Since the absolute minimum interval is 0 days, no actual interval check performed.
	then
		Include the reason for shot $currentShot Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 Rules for Recurring Td (CVX 09) END
*************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 Evaluation of Adolescent Tdap Shot START
 All pertussis-containing vaccines in the DTP vaccine group are allowed as the Adolescent Tdap. 
 Absolute Minimum Age = 7 years
 Absolute Minimum Interval:
     + If final dose in the primary series (or accepted/extra dose, if an extra dose exists) is a pertussis-containing dose, then absolute minimum interval = 4 weeks 
     + If final dose in the primary series (or accepted/extra dose, if an extra dose exists) is a non-pertussis-containing dose, then absolute minimum interval = 0 days
 When the target dose is the Adolescent Tdap, if the shot is administered after the primary series is complete but does not meet the criteria for the Adolescent Tdap dose, then the Evaluation is Accepted and the reason code is EXTRA_DOSE. 
 If the shot meets the criteria for the Adolescent Tdap dose, then the Evaluation is Valid. 
 If the patient has received an Adolescent Tdap dose at >= 7 years and < 10 years, then the absolute minimum age for the second Adolescent Tdap dose = 10 years. 
 If the patient has received their first Adolescent Tdap dose at >= 7 years and < 10 years, evaluate subsequent shots >= 7 years and < 10 years as Accepted/Extra Dose. 
*************************************************************************************************************************************************************************************/

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Adolescent Tdap Shot >= 7yrs and < 10yrs START
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "DTP(Abstract): Evaluate Adolescent Tdap Shot >= 7yrs and < 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an IceFact $iceResultFinding
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the series is complete
		The patient information $patientInformation must be known to complete writing this rule
			- make note of the date as $dtDateAtAge7 when the patient is "7y" of age
			- make note of the date as $dtDateAtAge10 when the patient is "10y" of age
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Series $targetSeries
			- the administration date of the shot is >= $dtDateAtAge7
			- the administration date of the shot is < $dtDateAtAge10
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"			
			- make note of the date this shot was administered as $dtAdministrationDateOfShotBeingEvaluated
	then
		// Abstract Rule- just record it was processed 
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


//////////////////////////////////////////////////////////////////
// First Adolescent Tdap Shot >= 7yrs and < 10yrs START
//////////////////////////////////////////////////////////////////

rule "DTP: Evaluate First Adolescent Tdap Shot based on Age >= 7yrs and < 10yrs" extends "DTP(Abstract): Evaluate Adolescent Tdap Shot >= 7yrs and < 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There does not exist an Administered Shot
			- the Shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the Shot is >= $dtDateAtAge7
			- the Administration Date of the Shot is <= $dtAdministrationDateOfShotBeingEvaluated
			- make note of the Diseases Targeted by the Vaccine Administered as $diseasesTargetedByThisPriorShot
			- the collection $diseasesTargetedByThisPriorShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisPriorShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisPriorShot contains "SUPPORTED_DISEASE_CONCEPT.037"
	then
		Include the Reason for Shot $currentShot Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Evaluate First Adolescent Tdap Shot as Accepted / Extra_Dose if Interval of 4 weeks between Pertussis shots is not met" extends "DTP: Evaluate First Adolescent Tdap Shot based on Age >= 7yrs and < 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $previousShot
			- the Shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the Administration Date of the Shot is <= $dtAdministrationDateOfShotBeingEvaluated
			- make note of the Diseases Targeted by the Vaccine Administered as $diseasesTargetedByThisPriorShot
			- the collection $diseasesTargetedByThisPriorShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- make note of the Date this Shot was Administered as $dtAdministrationDatePrior
		Confirm Elapsed Time between $dtAdministrationDatePrior and $dtAdministrationDateOfShotBeingEvaluated < "28d"
	then
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Do not run the default Extra_Dose series rule, which would mark this First Adolescent Tdap Shot as an Extra Dose" extends "DTP: Evaluate First Adolescent Tdap Shot based on Age >= 7yrs and < 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		// Override default extra dose check for first adolescent Tdap so that it is not marked as an Extra Dose
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////
// First Adolescent Tdap Shot >= 7yrs and < 10yrs END
//////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////
// Remaining Adolescent Tdap Shot >= 7yrs and < 10yrs START
//////////////////////////////////////////////////////////////////

rule "DTP: Evaluate Remaining Adolescent Tdap Shots >= 7yrs and < 10yrs as Accepted / Extra Dose" extends "DTP(Abstract): Evaluate Adolescent Tdap Shot >= 7yrs and < 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There exists an administered shot
			- the shot belongs to the Series $associatedTargetSeries
			- the shot does not belong to the primary series
			- that has already been evaluated and whose Shot Validity is VALID
			- the administration date of the shot is >= $dtDateAtAge7
			- the administration date of the shot is <= $dtAdministrationDateOfShotBeingEvaluated
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisPriorShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
	then
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Do not run the default Interval series rule" extends "DTP: Evaluate Remaining Adolescent Tdap Shots >= 7yrs and < 10yrs as Accepted / Extra Dose"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		// No need to run default series extra dose rule, as the rule this extends already marks it as an Extra Dose
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Do not run the default Extra_Dose series rule, which would mark this Remaining Adolescent Tdap shot as an Extra Dose" extends "DTP: Evaluate Remaining Adolescent Tdap Shots >= 7yrs and < 10yrs as Accepted / Extra Dose"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		// No need to run default series extra dose rule, as the rule this extends already marks it as an Extra Dose
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////
// Remaining Adolescent Tdap Shot >= 7yrs and < 10yrs END
//////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Adolescent Tdap Shot >= 7yrs and < 10yrs END
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Adolescent Tdap Shot >= 10yrs START
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "DTP: Evaluate Adolescent Tdap Shot based on Age >= 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an IceFact $iceResultFinding
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is Complete
		The patient information $patientInformation must be known to complete writing this rule
			- make note of the date as $dtDateAtAge10 when the patient is "10y" of age
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Series $targetSeries
			- the administration date of the shot is >= $dtDateAtAge10
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
			- make note of the date this shot was administered as $dtAdministrationDateOfShotBeingEvaluated
	then
		Include the Reason for Shot $currentShot Valid
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Evaluate First Adolescent Tdap Shot >= 10yrs as Accepted / Extra_Dose if Interval of 4 weeks between Pertussis Shots is not met" extends "DTP: Evaluate Adolescent Tdap Shot based on Age >= 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $previousShot
			- the Shot belongs to the Series $targetSeries
			- that has already been Evaluated
			- the Administration Date of the Shot is <= $dtAdministrationDateOfShotBeingEvaluated
			- make note of the Diseases Targeted by the Vaccine Administered as $diseasesTargetedByThisPriorShot
			- the collection $diseasesTargetedByThisPriorShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- make note of the Date this Shot was Administered as $dtAdministrationDatePrior
		Confirm Elapsed Time between $dtAdministrationDatePrior and $dtAdministrationDateOfShotBeingEvaluated < "28d"
	then
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "DTP: Do not run the default Extra_Dose series rule, which would mark this Adolescent Tdap Shot >= 10 yrs as an Extra Dose" extends "DTP: Evaluate Adolescent Tdap Shot based on Age >= 10yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		// Override default extra dose check for first adolescent Tdap so that it is not marked as an Extra Dose
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Adolescent Tdap Shot >= 10yrs START
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

rule "DTP: If Patient received a Tdap Dose >= 10yrs, make note of this via IceFact of ADOLESCENT_TDAP_COMPLETED"
 	dialect "mvel"
 	agenda-group "HistoryEvaluation^customEvaluationRule"
 	auto-focus true
 	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
		There is an IceFact $pertussisDoseFact
			- that has Finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has associated Series $targetSeries
			- make note of the associated Administered Shot as $pertussisDose			
		The Patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "10y" of age
		There is an Administered Shot $samePertussisDose
			- that is the same Shot as $pertussisDose
			- the Administration Date of the Shot is >= $dtDateAtAge
			- make note of the Diseases targeted by the Vaccine Administered as $diseasesTargetedByThisShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
 	then
		Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_COMPLETED.getConceptCodeValue() with TargetSeries $targetSeries into working memory
		Record that this dose rule was processed for the TargetDose $pertussisDose
		Log that this dose rule fired for the dose $pertussisDose in the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 Evaluation of Adolescent Tdap Shot END
*************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 Definition of "A dose of pertussis" START
 A dose of pertussis is defined as one of the following:
     * A primary series (3-dose series or 5-dose series) dose of CVX 01, 20, 106, 107, or 115
     * A primary series shot of CVX 01, 20, 106, 107, or 115 where the shot is evaluated as Invalid with a reason code of D_AND_T_INVALID/P_VALID 
     * An Adolescent Tdap dose of CVX 01, 20, 106, 107, or 115    (adolescent Tdap series is if series is complete and patient >= 7yrs)
*************************************************************************************************************************************************************************************/

rule "DTP(1): A primary series dose of pertussis exists if a (valid) dose of Pertussis was administered"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	auto-focus true
	when
		There is an administered shot $currentShot
			- that has already been evaluated and whose shot validity is VALID
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisShot
			- the shot belongs to the primary series
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200"
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has associated administered shot $currentShot
	then
		Insert an IceFact SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() with TargetDose $currentShot into working memory
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot
end


rule "DTP(2): A primary series dose of pertussis exists if a shot that was evaluated as Invalid with a reason code of D_AND_T_INVALID/P_VALID"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	auto-focus true
	when
		There is an administered shot $currentShot
			- that has already been evaluated and whose shot validity is INVALID
			- make note of all evaluation reasons for this shot as $collectionOfStrReasons
			- the shot belongs to the primary series
			- the collection $collectionOfStrReasons contains "EVALUATION_REASON_CONCEPT.D_AND_T_INVALID/P_VALID"
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200"
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has associated administered shot $currentShot
	then
		Insert an IceFact SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() with TargetDose $currentShot into working memory
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot
end


rule "DTP(3): A dose of pertussis exists if a (valid) dose for Adolescent Tdap was administered"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	auto-focus true
	when
		The Patient information $evaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "7y" of age	
		There is an administered shot $currentShot
			- that has already been evaluated and whose shot validity is VALID
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200"
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
			- the administration date of the shot is >= $dtDateAtAge
			- make note of the Associated Series as $associatedTargetSeries
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has associated administered shot $currentShot
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the series is complete
	then
		Insert an IceFact SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() with TargetDose $currentShot into working memory
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot
end

/*************************************************************************************************************************************************************************************
 Definition of "A dose of pertussis" END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 Absolute Minimum Vaccine Age for Tdap Doses 1-3 & 4-5 in the 5-dose Series (including associated 5-dose series exception rules) START
/************************************************************************************************************************************************************************************/

// The absolute vaccine minimum age for Tdap (CVX 115) applies when Tdap/Td is given as target dose 1, 2, or 3 in the 5-dose series.
//     * If Tdap is administered below the CVX code absolute minimum age, then the Evaluation is Invalid and the reason code is INSUFFICIENT_ANTIGEN.
rule "DTP: Absolute Minimum Vaccine Age for Tdap applies for target doses 1-3 of the 5-dose series when certain conditions are true"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP5DoseSeries"
			- the dose number in the series is <= 3
			- the vaccine administered is "ICE115"
			- make note of the minimum vaccine age for this shot as $strValidMinimumAge
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The Patient information $evaluationPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and $administrationDate < $strValidMinimumAge
	then
		Include the reason for shot $currentShot Invalid due to "Insufficient Antigen"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


// The absolute vaccine minimum age does not apply when Tdap (CVX 115) is administered as target dose 4 or 5 in the 5-dose series. 
//     * The absolute minimum age is only waived when the target dose is a true dose 4 or 5; the waiver does not apply when dose 1 was skipped. 
rule "DTP(Abstract): Absolute Minimum Vaccine Age for Tdap check for target doses 4 or 5 of the 5-dose series"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP5DoseSeries"
			- the series that the shot belongs to is not complete
			- the dose number in the series is one of (4,5)
			- the vaccine administered is "ICE115"
			- make note of the Associated Series as $associatedTargetSeries
	then
		// Simply log that this occurred
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end
			
			
rule "DTP: Absolute Minimum Vaccine Age for Tdap does not apply for true target dose 4 or 5 of the 5-dose for 5-dose series if 5-dose Exception #1 did not occur" 
		extends "DTP(Abstract): Absolute Minimum Vaccine Age for Tdap check for target doses 4 or 5 of the 5-dose series"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There does not exist an IceFact
			- that has associated administered shot $currentShot
			- that has finding SupportedFactConcept._DTP_5_DOSE_SERIES_EXCEPTION1.conceptCodeValue
	then
		// Minimum age for vaccine is waived - Simply log that this occurred
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end
		
		
rule "DTP: Absolute Minimum Vaccine Age for Tdap does not apply for true target dose 4 or 5 of the 5-dose for 5-dose series if 5-dose Exception #2 did not occur" 
		extends "DTP(Abstract): Absolute Minimum Vaccine Age for Tdap check for target doses 4 or 5 of the 5-dose series"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There does not exist an IceFact
			- that has associated administered shot $currentShot
			- that has finding SupportedFactConcept._DTP_5_DOSE_SERIES_EXCEPTION2.conceptCodeValue
	then
		// Minimum age for vaccine is waived - Simply log that this occurred
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
 Absolute Minimum Vaccine Age for Tdap Doses 1-3 & 4-5 in the 5-dose Series (including associated 5-dose series exception rules) END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 CVX Specific Rules for Td (CVX 09, 113, 138, 139) START
/************************************************************************************************************************************************************************************/

// If Td (CVX 09, 113, 138, 139) is administered below the CVX Absolute Minimum Age (< 7 years - 4 days), evaluate as Invalid/BELOW_MINIMUM_AGE_VACCINE.
// Ignore the reported Td (CVX 09, 113, 138, 139) when determining the recommended due date for the next target dose due and when evaluating the next shot based on the 
// minimum interval for that target dose.
rule "DTP: If Absolute Vaccine Minimum Age for Td is not met (7y-4d), shot should be ignored when calculating intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200"
			- the vaccine administered a member of ("ICE09", "ICE113", "ICE138", "ICE139", "ICE196")
			- make note of the minimum vaccine age for this shot as $strValidMinimumAge
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The Patient information $evaluationPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and $administrationDate < $strValidMinimumAge
	then
		Mark the shot $currentShot as Ignored
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


///////
// CVX-specific Rule for Td (CVX 09, 113, 138, 139) - If a patient is in the DTP 5-dose series and a Valid Td is administered, return the reason code SUPPLEMENTAL_TEXT and descriptive text 
// that Pertussis is needed to complete the series
///////
rule "If a patient is in the DTP 5-dose series and a Valid Td is administered, return the reason code SUPPLEMENTAL_TEXT and descriptive text that Pertussis is needed to complete the series"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP5DoseSeries"
			- that has already been evaluated and whose Shot Validity is VALID
			- the Shot belongs to the Primary Series 
			- the vaccine administered a member of ("ICE09", "ICE113", "ICE138", "ICE139", "ICE196")
			- make note of the minimum vaccine age for this shot as $strValidMinimumAge
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the Administered Shot Number as $administeredShotNumber
			- make note of the Associated Series as $associatedTargetSeries
		There does not exist an administered shot
			- the Shot belongs to the Series $associatedTargetSeries
			- that has already been Evaluated
			- the Administered Shot Number is > $administeredShotNumber
		The Patient information $evaluationPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and $administrationDate >= $strValidMinimumAge
	then
		Include Supplemental Text "Pertussis is needed to complete the series." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
 CVX Specific Rules for Td (CVX 09, 113, 138, 139) START
/************************************************************************************************************************************************************************************/
 
 
/*************************************************************************************************************************************************************************************
 CVX Specific Rules for DT (CVX 28) START
 If a patient is administered DT (CVX 28) at:
    + <=7 years, evaluate the shot as Valid and return evaluation reason code SUPPLEMENTAL_TEXT and Descriptive Text: "DT should only be administered to children 6 weeks through 
      6 years of age with a contraindication to pertussis vaccine."
    + > 7 years, evaluate the shot as Valid and return evaluation reason code SUPPLEMENTAL_TEXT and Descriptive Text: "Pertussis is needed to complete the series."
/************************************************************************************************************************************************************************************/
 
 rule "If a patient is administered a Valid DT (CVX 28) <= 7yrs, return the reason code SUPPLEMENTAL_TEXT and descriptive text indicating limitations of when DT should be administered"
 	dialect "mvel"
 	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Shot belongs to the Primary Series
			- that has already been Evaluated and whose Shot Validity is VALID
			- the Vaccine Administered is "ICE28"
			- make note of the Date this Shot was Administered as $administrationDate
 			- make note of the Administered Shot Number as $administeredShotNumber
			- make note of the Associated Series as $associatedTargetSeries
		There does not exist an administered shot
			- the Shot belongs to the Series $associatedTargetSeries
			- that has already been Evaluated
			- the Administered Shot Number is > $administeredShotNumber
		The Patient information $evaluationPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and $administrationDate <= "7y"
	then
		Include Supplemental Text "DT should only be administered to children 6 weeks through 6 years of age with a contraindication to pertussis vaccine." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end


rule "If a patient is administered a Valid DT (CVX 28) > 7yrs, return the reason code SUPPLEMENTAL_TEXT and descriptive text indicating limitations of when DT should be administered"
	dialect "mvel"
 	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Shot belongs to the Primary Series
			- that has already been Evaluated and whose Shot Validity is VALID
			- the Vaccine Administered is "ICE28"
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the Administered Shot Number as $administeredShotNumber
			- make note of the Associated Series as $associatedTargetSeries
		There does not exist an administered shot
			- the Shot belongs to the Series $associatedTargetSeries
			- that has already been Evaluated
			- the Administered Shot Number is > $administeredShotNumber
		The Patient information $evaluationPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and $administrationDate > "7y"
	then
		Include Supplemental Text "Pertussis is needed to complete the series." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
 CVX Specific Rules for DT (CVX 28) END
/************************************************************************************************************************************************************************************/



/*************************************************************************************************************************************************************************************
 CVX Code Minimum Interval Between Tdap/DTaP and Td/DT START
/************************************************************************************************************************************************************************************/

// CVX Code Minimum Interval Between Tdap/DTaP and Td/DT
// If Tdap (CVX 115), DTaP (CVX 20, 106, or 107), or DTP (CVX 01) is given below the minimum interval from a Td (CVX 09, 113, 138, 139) or DT (CVX 28), but the shot meets the 
// minimum age, then the Tdap/DTaP/DTP is evaluated as Invalid and the reason code is D_AND_T_INVALID/P_VALID  (i.e., Diphtheria and tetanus components invalid due to minimum 
// interval violation, pertussis component valid).
rule "DTP: Enforce Minimum Interval between Tdap/DTaP and Td/DT if the shot meets the minimum age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	no-loop true
	when
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200"
			- the administered shot number is >= 2
			- make note of the administered shot number as $nAdministeredShotNumber
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByThisShot
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByThisShot contains "SUPPORTED_DISEASE_CONCEPT.037"
			- make note of the associated series as $targetSeries
		There is an Administered Shot $administeredPrior
			- the shot belongs to the Series $targetSeries
			- the administered shot number is == ($nAdministeredShotNumber-1)
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedByPriorShot
			- the collection $diseasesTargetedByPriorShot contains "SUPPORTED_DISEASE_CONCEPT.032.9"
			- the collection $diseasesTargetedByPriorShot contains "SUPPORTED_DISEASE_CONCEPT.037"
			- the collection $diseasesTargetedByPriorShot does not contain "SUPPORTED_DISEASE_CONCEPT.033.9"
		There does not exist an IceFact
			- that has associated administered shot $currentShot
			- that has finding SupportedFactConcept._BELOW_MINIMUM_AGE.conceptCodeValue
		There does not exist an IceFact
			- that has associated administered shot $currentShot
			- that has finding SupportedFactConcept._INVALID_VACCINE.conceptCodeValue
		There is an IceFact $ICEFactTypeFinding
			- that has associated administered shot $currentShot
			- that has finding SupportedFactConcept._BELOW_MINIMUM_INTERVAL.conceptCodeValue
	then
		Remove Evaluation Reason "EVALUATION_REASON_CONCEPT.BELOW_MINIMUM_INTERVAL" from Shot $currentShot
		Retract IceFact $ICEFactTypeFinding from Working Memory
		Include the reason for shot $currentShot Invalid due to "D_AND_T_INVALID/P_VALID"
		Refresh all Facts in the Series $targetSeries for Evaluation
		Refresh all Facts in the Shot $currentShot
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 CVX Code Specific Rules END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 5-Dose Series Special Rules 
 Rule for Recommending/Evaluating Next Dose From Invalid Tdap/TD Given Below Minimum Age for Vaccine
 If Tdap (CVX 115) is given as target dose 1, 2 or 3 to a patient <7 years - 4 days old, then the invalid shot should be ignored when determining the next target dose due and 
 	when evaluating the next shot based on the minimum interval for that target dose.
*************************************************************************************************************************************************************************************/

rule "DTP(HistoryEvaluation): If Tdap (CVX 115) is administered as target dose 1, 2 or 3 to a patient < 7yrs-4days, shot should be ignored when calculating intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		// The shot being evaluated
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP5DoseSeries"
			- the dose number in the series is <= 3
			- the vaccine administered is "ICE115"
			- make note of the date this shot was administered as $administrationDate
			- make note of the minimum vaccine age for this Shot as $validMinimumAgeVaccine
			- make note of the associated series as $associatedTargetSeries
		The Patient information $evaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		// Check that Td or Tdap was admininstered < 7y-4d of age
		Confirm elapsed time between $birthdate and $administrationDate < $validMinimumAgeVaccine
	then
		Mark the shot $currentShot as Ignored
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
 5-Dose Series Special Rules END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
5-dose Series Exception Rules
The 5-dose series above applies unless:

Exception 1: Series Complete with 3 Doses
    * If the patient is >=7, received the first dose at >= 12 months of age, and received at least one dose at >= 4 years of age, then the 
      series is complete with 3 doses (doses 2, 3 and 4).
      
Exception 2: Series Complete with 4 Doses
    * If 4 doses have been administered and the 4th dose is administered at >= 4 years of age and the interval between dose 3 and dose 4 >= 6m-d, then the series is complete 
      with 4 doses; the 5th (booster) dose is not needed.
 *************************************************************************************************************************************************************************************/
rule "DTP: (5-Dose Series Exception Rule 1)- Skip Dose Number to 4 if Patient >= 7 yrs when 3rd target dose is being administered, with first dose at >= 12m and at least 1 dose at >= 4yrs"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP5DoseSeries"
			- the dose number in the Series is == 3
			- make note of the date this shot was administered as $assign_dtAdministrationDate
			- make note of the associated series as $targetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $birthdate
			- make note of the date as $dtDateAtAge4y when the patient is "4y" of age
			- make note of the date as $dtDateAtAge12m when the patient is "12m" of age
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge12m
			- the dose number in the Series is == 1
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge4y
			- the dose number in the Series is == 2
		Confirm elapsed time between $birthdate and $assign_dtAdministrationDate >= "7y"
	then
		Set Dose Number of $currentShot to 4
		Skip Series Dose Number to 4 from 3 for all diseases in the Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "DTP: (5-Dose Series Exception Rule 1)- Mark the 5-Dose Series Complete if age >=7 yrs, with the first dose at >= 12m and at least 1 dose at >= 4yrs"
 	dialect "mvel"
 	agenda-group "HistoryEvaluation^postEvaluationCheck"
 	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP5DoseSeries"
			- the Series is not Complete
			- the number of Doses Administered is == 3
		The Patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
			- make note of the Date as $dtDateAtAge4y when the Patient is "4y" of age
			- make note of the Date as $dtDateAtAge12m when the Patient is "12m" of age
			- make note of the Date as $dtDateAtAge7y when the Patient is "7y" of age
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the Shot is >= $dtDateAtAge12m
			- the Dose Number in the Series is == 1
		There exists an Administered Shot
			- the Shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge4y
			- the Dose Number in the Series is >= 2
		There is an Administered Shot $thirdDose
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 3
			- that has already been Evaluated and whose Shot Validity is VALID
			- make note of the Date this Shot was Administered as $dtDateThirdDose
		There does not exist an Administered Shot
			- the Shot belongs to the Series $targetSeries
			- that has not already been Evaluated
			- the Administration Date of the Shot is >= $dtDateThirdDose
			- the Administration Date of the Shot is < $dtDateAtAge7y
		Confirm the Date evalTime is on the same date or after $dtDateAtAge7y
 	then
 		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for Evaluation
		Insert an IceFact SupportedFactConcept._DTP_5_DOSE_SERIES_EXCEPTION1.getConceptCodeValue() into working memory
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end
 
 
rule "DTP: (5-Dose Series Exception Rule 2)- Mark the 5-Dose Series Complete if the 4th Dose is Administered at >= 4 yrs of Age and the Interval between Dose 3 and Dose 4 >= 6m-4d"
 	dialect "mvel"
 	agenda-group "HistoryEvaluation^postEvaluationCheck"
 	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP5DoseSeries"
			- the Series is not Complete
			- the number of Doses Administered is == 4
		The Patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the Date as $dtDateAtAge when the patient is "4y" of age
		There is an Administered Shot $dose4
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 4
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge
			- make note of the Date this Shot was Administered as $dtDose4Administered
		There is an Administered Shot $dose3
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 3
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the Date this Shot was Administered as $dtDose3Administered
		Confirm elapsed time between $dtDose3Administered and $dtDose4Administered >= "6m-4d"
 	then
 		Mark the Series $targetSeries Complete
 		Insert an IceFact SupportedFactConcept._DTP_5_DOSE_SERIES_EXCEPTION2.getConceptCodeValue() into working memory
		Refresh all Facts in the Series $targetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end
 
/*************************************************************************************************************************************************************************************
 5-Dose Series Exception Rules END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 3-dose Series Exception Rules START:
    + If the patient has received 3 doses and none of the doses are pertussis-containing vaccines, the DTP 3-dose series is NOT complete. 
    + Recommend Tdap (CVX 115) at recommended interval = 0 days.  (*Note*: not implemented here; see Recommendation^DTP file for this rule.) 
 Example: Patient age = 10 years. CVX 09 (Td) given at 7 years, 8 years, and 10 years. Recommend final dose Tdap (CVX 115) at 10 years. 
/************************************************************************************************************************************************************************************/

rule "DTP: (3-Dose Series Exception Rule[Series Not Complete])- If the Patient has Completed the Series but none of the Doses are pertussis-containing Vaccines, mark the 3-dose Series Not Complete"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	dialect "mvel"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP3DoseSeries"
			- the Series is Complete
		There does not exist an IceFact
			- that has Finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has Associated Series $targetSeries
	then
		Manually Mark the Series $targetSeries Not Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "DTP: (3-Dose Series Exception Rule[Evaluation])- If none of the prior 3 or more Doses administered in 3-dose Series contains pertussis but this Shot does contain pertussis, evaluate it as Valid and mark the Series Complete"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck" // This activation-group prevents the default series interval rule from firing, which would mark this shop as ACCEPTED/EXTRA_DOSE instead of simply valid.
	dialect "mvel"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200" and the Series with name "DTP3DoseSeries"
			- the Dose Number in the Series is > 3
			- the Shot belongs to the Primary Series
			- make note of the Diseases targeted by the Vaccine Administered as $diseasesTargeted
			- the collection $diseasesTargeted contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- make note of the Associated Series as $targetSeries
		There does not exist an IceFact
			- that has Finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has Associated Series $targetSeries
	then
		Set the Shot Status of $currentShot to Valid
		Mark the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 3-Dose Series Exception Rules END
/************************************************************************************************************************************************************************************/
