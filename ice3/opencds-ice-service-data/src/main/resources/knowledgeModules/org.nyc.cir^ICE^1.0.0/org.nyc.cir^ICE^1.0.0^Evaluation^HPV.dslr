/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine
import org.cdsframework.ice.supportingdata.BaseDataPerson

expander ../org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext
global org.cdsframework.ice.service.Schedule schedule


// If CVX 118 (HPV, bivalent (Cervarix) is reported for a male patient, then evaluate as Accepted and the reason code is VACCINE_NOT_LICENSED_FOR_MALES.
// Mark that the shot should be ignored when determining further evaluations and recommendations
rule "HPV: Evaluate CVX 118 administered to male patient as Accepted with reason VACCINE_NOT_LICENSED_FOR_MALES; Ignore shot for completion of the Series" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.840"
			- the vaccine administered is "ICE118"
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- the patient is male
	then
		Mark the shot $currentShot as Ignored
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Licensed For Males"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "Evaluate HPV Shot As Accepted If Patient >=27 yrs and does not have one or more Doses <27 yrs" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.840"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "27y" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.840"
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "27y"
	then
		Include the reason for shot $currentDose Accepted due to "Above Recommended Age"
		Mark that evaluation of shot $currentDose is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "HPV: Evaluate the Absolute Minimum Interval in 3-Dose Series between Doses 1 and 3 - 5m-4 days required" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when  
		There is an Administered Shot $currentDose that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.840" and the Series with name "HPV3DoseSeries"
			- the dose number in the series is == 3
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
			- make note of the Associated Series as $associatedTargetSeries
		There is an Administered Shot $previousDose
			- the shot belongs to the Series $associatedTargetSeries
			- the dose number in the series is == 1
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $dtAdministrationDatePreviousDose
		Confirm Elapsed time between $dtAdministrationDatePreviousDose and $dtAdministrationDateCurrentDose < "5m-4d"
	then
		Include the reason for shot $currentDose Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $associatedTargetSeries
end

