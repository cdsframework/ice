/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdata.BaseDataEvaluationReason
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


rule "Hib: Mark Shot with Invalid Vaccine as Invalid but do _not_ indicate VACCINE_NOT_ALLOWED_FOR_DOSE reason (overrides base vaccine check rule)" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300"
			- make note of the administered vaccine as $administeredVaccine
			- make note of the dose number as $doseNumber
			- make note of the Associated Series as $associatedTargetSeries
		There is a series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the vaccine $administeredVaccine is not permitted for dose number $doseNumber in this Series
			- make note of the number of doses required to complete this series as $numberOfDosesInSeries
			- the numeric $doseNumber is <= $numberOfDosesInSeries
	then
		Include the reason for shot $currentShot Invalid for this Series
		Record that this Dose Rule was Processed for the TargetDose $currentShot
		Log that this Dose Rule fired for the Dose $currentShot in the Series $targetSeries
end


rule "Evaluate Hib Shot As Accepted If Patient >=5 Yrs and Series Not Complete by 5yrs of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	salience 150
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the target dose number for this shot as $assign_nDoseNumber
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "5y" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"
	then
		Include the reason for shot $currentDose Accepted due to "Above Recommended Age for Series"
		Manually Mark the Series $targetSeries Not Complete
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "Hib: Cancel Extra Dose Rule for Shot if Patient >=5 Yrs and Series Not Complete by 5yrs of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	salience 150
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the target dose number for this shot as $assign_nDoseNumber
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "5y" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"
	then
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end	


rule "Hib:Evaluate Hib Shot as Below Minimum Age for Final Dose of 4-Dose Hib Series if 4th Dose Administered <361 Days of Age and Received 0 Doses Administered Prior to 7months of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	salience 150
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- the Dose Number in the Series is == 4
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "7m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Doses Administered before $dateOfInterest is == 0			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "361d"	
	then
		Include the reason for shot $currentDose Invalid due to "Below Minimum Age for Final Dose"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end



rule "Hib: Allow Hib Booster Vaccine For Last Dose of 4-Dose Hib Series if 4th Dose Administered >=361 Days and Prior Doses Administered" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	salience 150
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Vaccine Administered is "ICE50"
			- make note of the target dose number for this shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Doses Required to Complete this Series is <= $assign_nDoseNumber
			- the Number of Doses Administered is > 0
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "361d"
    then
        //actions
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end

rule "Hib: Allow Hib Booster Vaccine if Patient is >= 5yrs" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	salience 150
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300"
			- the Vaccine Administered is "ICE50"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"						
	then
        //actions
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $associatedTargetSeries
end


rule "Hib: Booster Vaccine Not Allowed (Booster only) for patients less than 5y of age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	salience 140
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Vaccine Administered is "ICE50"
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "5y" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "5y"
	then
		Include the reason for shot $currentDose Invalid due to "Vaccine not Permitted for this Dose (Booster Only)"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "Hib: Set Dose Number to 2 in 4-Dose Hib Series if No Shots Administered and Patient Between 7 and 12 Months" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300" 
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Administered Shots is == 0
			- make note of the Effective Dose Number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 2
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 2 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Hib: Set Dose Number to 3 in 4-Dose Hib Series if No Shots Administered and Patient Between 12 and 15 Months" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Administered Shots is == 0
			- make note of the Effective Dose Number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 3
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "15m"	
	then
		Skip Series Dose Number to 3 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Hib: Set Dose Number to 4 in 4-Dose Hib Series if No Shots Administered and Patient Between 15 Months and 5 Years" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Administered Shots is == 0
			- make note of the Effective Dose Number in the Series as $effectiveDoseNumber
			- the numeric $effectiveDoseNumber is < 4
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "15m" && elapsed time between $dtBirthDate and evalTime < "5y"	
	then
		Skip Series Dose Number to 4 from $effectiveDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Hib: Skip Dose Number to 2 in 4-Dose Hib Series for Patient Between Between 7and12 Months" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Dose Number in the Series is < 2
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m"
	then
		Set Dose Number of $currentDose to 2
		Skip Series Dose Number to 2 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


rule "Hib: Skip Dose Number to 4 in 4-Dose Hib Series if Patient Between 7and12 Months and has Received 1 Dose < 7 Months of Age and 1 Dose <= 12m-28d of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Dose Number in the Series is < 3
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the number of Doses Administered is == 1
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m-28d"	
	then
		Set Dose Number of $currentDose to 3
		Skip Series Dose Number to 3 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


rule "Hib: Skip Dose Number to 3 in 4-Dose Hib Series if Patient Between 12and15 Months and has Received <2 Doses Administered at <12 Months of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Dose Number in the Series is < 3
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Doses Administered before $dateOfInterest is < 2			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "15m"
	then
		Set Dose Number of $currentDose to 3
		Skip Series Dose Number to 3 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Hib: Skip Dose Number to 4 in 4-Dose Hib Series if Patient Between 12and15 Months and has Received 2 Doses Administered at <12 Months of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.300"
			- the name of the Series is "Hib4DoseSeries"
			- the Number of Doses Administered before $dateOfInterest is == 2
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "15m"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Hib: Skip Dose Number to 4 in 4-Dose Hib Series for Patient over 15 Months" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.300" and the Series with Name "Hib4DoseSeries"
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "15m"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.482.2" in Series $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end
