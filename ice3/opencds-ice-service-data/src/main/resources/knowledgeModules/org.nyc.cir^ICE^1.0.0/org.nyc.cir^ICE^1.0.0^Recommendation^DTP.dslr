/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0 

import java.util.Date
import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext
global org.cdsframework.ice.service.Schedule schedule


/*************************************************************************************************************************************************************************************
General Vaccine Recommendation rules START

// Generally, ICE recommends at the group level rather than a specific CVX code; however, DTP vaccine group recommendations are at the CVX level.
// When recommending DTP as part of the 3-dose or 5-dose series, the rules below state which CVX code to recommend. For patients >=7 years of age, 
// the rules below also include an override to the series routine recommended age.
// If the patient is < 7 years, always recommend DTaP NOS (CVX 107).
// If a patient is >=7 years and
//     * Has received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Td (CVX 09).
//     * Recommended Age Override (overrides series table routine recommended age for next dose) :
//         + Recommended Age = 7 years
//         + Recommended interval follows table rules
// If patient is >= 7yrs and has NOT received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Tdap (CVX 115).
//     * Recommended Age Override (overrides series table routine recommended age for next dose): 
//         + Recommended Age = 7 years
//         + Recommended interval follows table rules
*************************************************************************************************************************************************************************************/

// If the patient is < 7 years, always recommend DTaP NOS (CVX 107).
rule "DTP: Recommend at the vaccine level in DTP series; if the patient < 7yrs and series is not complete, always recommend DTaP NOS (CVX 107)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is not complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
		Confirm elapsed time between $dtBirthdate and evalTime < "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE107"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If a patient is >=7 years; has _not_ completed the series and has received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Td (CVX 09).
rule "DTP: Recommend Td (CVX 09) at 7yrs of age if series is not complete, patient >= 7yrs, has received a dose of pertussis >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is not complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the series $targetSeries as well as IceResult administration date >= $dtDateAtAge
		Confirm elapsed time between $dtBirthdate and evalTime >= "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE09"
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If a patient is >=7 years, has _not_ completed the series and has _not_ received a dose of pertussis at >=7 years of age, then the next dose in the series should be Tdap (CVX 115).
rule "DTP: Recommend Tdap (CVX 115) at age 7yrs if series is not complete, if patient >= 7yrs and has _not_ received a dose of Pertussis >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"			// override series default age rule
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is not complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the series $targetSeries as well as IceResult administration date >= $dtDateAtAge
		Confirm elapsed time between $dtBirthdate and evalTime >= "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE115"
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
General Vaccine Recommendation rules END
/************************************************************************************************************************************************************************************/

/**************************************************************************************************************************************************************************************************************************************
Recommendation Rules for Adolescent Tdap START

For patients who completed the primary DTP series (3-dose or 5-dose):
1) If the patient has NOT received a pertussis-containing dose at >= 7 years and <10 years and
    + any one of the below exception rules apply (for 5-dose series only), then recommend Tdap (CVX 115) at 7 years old (or apply recommended interval, if applicable). 
    + none of the below exception rules apply (for 5-dose series only), then recommend Tdap (CVX 115) at 11 years old (or apply recommended interval, if applicable).
2) If the patient has received a pertussis-containing dose at >= 7 years and <10 years, then recommend another Tdap (CVX 115) at 11 years old.
3) If the patient has received a pertussis-containing dose at >= 10 years, then recommend the recurring Td (CVX 09) every 10 years. 
/*************************************************************************************************************************************************************************************************************************************/

//////////////
// #1: For patients who completed the primary DTP series (5-dose), if the patient has NOT received a pertussis-containing dose at >= 7 years and <10 years and...:
//////////////

rule "DTP: If the 5-Dose Series is Complete and the patient has NOT received a pertussis containing dose at >= 7 years and <10 years, then invoke 5-Dose Series Adolescent Tdap Exception Rules" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
			- the name of the series is "DTP5DoseSeries"
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge7y when the patient is "7y" of age
			- make note of the date as $dtDateAtAge10y when the Patient is "10y" of age
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has Associated Series $targetSeries
			- make note of the date the Associated Shot was Administered as $pertussisDoseDate
			- the date $pertussisDoseDate >= $dtDateAtAge7y
			- the date $pertussisDoseDate < $dtDateAtAge10y
	then
		Set the Agenda Group Focus to "customRecommendationAdolescentTdapExceptionRule"
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


//////////////
// #2: For patients who completed the primary DTP series (3-dose or 5-dose): if the patient has received a pertussis-containing dose at >= 7 years and <10 years, then recommend
// another Tdap (CVX 115) at 11 years old. This is an adolescent Tdap dose.
//////////////

rule "DTP: If the primary series is complete and the patient has received a pertussis containing dose at >= 7 years and <10 years, then recommend another Tdap at at earliest date & recommended date of 11yrs, overdue date of 13yrs+4w" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge7y when the patient is "7y" of age
			- make note of the date as $dtDateAtAge10y when the Patient is "10y" of age
		There is an IceFact $pertussisFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has Associated Series $targetSeries
			- make note of the Associated Administered Shot as $pertussisDose
		There is an Administered Shot $targetPertussisDose
			- that is the same Shot as $pertussisDose
			- the Administration Date of the shot is >= $dtDateAtAge7y
			- the Administration Date of the shot is < $dtDateAtAge10y
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "11y" of age
			- make note of the date as $dtOverdueDateAtAge when the patient is "13y+4w" of age
	then
		// Create a Recommendation with earliest/recommended/latest ages of 11yrs/11yrs/13y+4w
		Create a Recommendation as $oRecommendationAge for the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Recommended Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Overdue Forecast Date for $oRecommendationAge to $dtOverdueDateAtAge
		Set the Recommendation Vaccine for $oRecommendationAge to "ICE115"
		Include the Recommendation $oRecommendationAge for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

//////////////
// If the patient has received a pertussis-containing dose at >= 10 years, then recommend the recurring Td (CVX 09) every 10 years. 
// Above rule encompasses this.
//////////////


/*************************************************************************************************************************************************************************************
Adolescent 5-Dose Series Tdap EXCEPTION RULES - START

For patients who completed the 5-dose series:
* Recommended Vaccine = Tdap (CVX 115) 
* Recommended Interval = 0 days (from dose 5 or an accepted/extra dose, if one exists) and Recommended Age = 11 years unless an exception applies.  

If any one of the following exceptions apply, then: 
    * Recommended Age = 7 years
    * Recommended Interval = 6 months from the last dose of pertussis; 0 days from the last non-pertussis dose. 
        + Exception A) The patient DOES NOT have a dose of pertussis at >= 4 years. 
        + Exception B) The patient DOES NOT have at least 4 doses of pertussis given at < 7 years
/************************************************************************************************************************************************************************************/

// For patients who completed the 5-dose series and NONE of the Tdap Adolescent Exceptions apply:
// * Recommended Vaccine = Tdap (CVX 115) 
// * Recommended Age = 11 years  
// * Recommended Interval = 6 months from the last dose of pertussis; 0 days from the last non-pertussis dose.
// * NOTE: No need to include a rule for the recommended interval in this section, as it is taken care of by the same recommendation interval rule below which encompasses all series (not just the 5-dose series) 
rule "DTP: If the 5-dose series is complete and a Tdap 5-dose series recommendation exception did _not_ occur, recommend Tdap at earliest date & recommended date of 11yrs, overdue date of 13yrs+4w" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationAdolescentTdapExceptionRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
			- the name of the series is "DTP5DoseSeries"
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There does not exist an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "11y" of age
			- make note of the date as $dtOverdueDateAtAge when the patient is "13y+4w" of age
	then
		// Create a Recommendation with earliest/recommended/latest ages of 11yrs/11yrs/13y+4w
		Create a Recommendation as $oRecommendationAge for the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Recommended Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Overdue Forecast Date for $oRecommendationAge to $dtOverdueDateAtAge
		Set the Recommendation Vaccine for $oRecommendationAge to "ICE115"
		Include the Recommendation $oRecommendationAge for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If an Adolescent Tdap needed and ANY Adolescent Tdap exception occurred, recommend an Adolescent Tdap at 7 years of age
rule "DTP: (Tdap 5-Dose Series Recommendation Exception Occurred)- If the 5-Dose Series is Complete and an Adolescent Tdap is required, recommend Adolescent Tdap at Earliest, Recommended & Overdue Date of 7yrs" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationAdolescentTdapExceptionRule"
	dialect "mvel"	
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the series is "DTP5DoseSeries"
			- the Series is complete
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
	then
		// Recommendation at age of 7yrs
		Create a Recommendation as $oRecommendationAge for the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Recommended Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Overdue Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Vaccine for $oRecommendationAge to "ICE115"
		Include the Recommendation $oRecommendationAge for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Exception A) The patient DOES NOT have a dose of pertussis at >= 4 years minus 4 days.
// Excepion A - higher activation priority than the general adolescent tdap recommendation rule above for 5-dose series 
rule "DTP: (Tdap 5-Dose Series Recommendation Exception A)- If patient does not have a dose of pertussis >= 4y-4d in the 5-Dose Series(Exception A), make note that the Adolescent Tdap Recommendation Exception criteria was met" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationAdolescentTdapExceptionRule"
	dialect "mvel"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
			- the name of the series is "DTP5DoseSeries"
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "4y-4d" of age
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the Series $targetSeries as well as IceResult administration date >= $dtDateAtAge
	then
		Logically Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.getConceptCodeValue() into working memory
		Log that this series rule fired for the Series $targetSeries
end


// Exception B) The patient DOES NOT have at least 4 doses of pertussis-containing vaccine given at < 7 years
// Excepion B - As with Exception A, higher activation priority than the general adolescent tdap recommendation rule above for 5-dose series 
rule "DTP: (Tdap 5-Dose Series Recommendation Exception B)- If the patient DOES NOT have at least 4 doses of pertussis given at < 7 years, make note that the Adolescent Tdap Recommendation Exception criteria was met" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationAdolescentTdapExceptionRule"
	dialect "mvel"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
			- the name of the series is "DTP5DoseSeries"
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge7Years when the patient is "7y" of age
		Verify that the count of IceFacts (where IceResult finding == SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() as well as IceResult administration date < $dtDateAtAge7Years) is < 4
	then
		Logically Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.getConceptCodeValue() into working memory
		Log that this series rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
Adolescent 5-Dose Series Tdap rules END
/************************************************************************************************************************************************************************************/

/*************************************************************************************************************************************************************************************
If the patient has completed the primary DTP series (3-dose or 5-dose) but has not received a dose of pertussis at >= 7 years, then an adolescent Tdap (CVX 115) is needed.

For patients who completed the 3-dose series (i.e., dose 1 at >= 7 years):
    * Recommended Age = N/A
    * Recommended Interval = 6 months from the last dose of pertussis; 0 days from the last non-pertussis dose.
/************************************************************************************************************************************************************************************/

//////////////
// See non-series specific rules below
//////////////

/*************************************************************************************************************************************************************************************
 Adolescent 3-Dose Series Tdap rules END
/************************************************************************************************************************************************************************************/

// If one or more of the Adolescent Tdap Exception Rules A or B apply, recommend an Adolescent Tdap 0 days from the last non-pertussis shot  
rule "DTP: If either the 3-Dose or 5-Dose Series is Complete and an Adolescent Tdap is required, recommend Tdap at earliest & recommended interval of 0 days from last non-pertussis dose" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
			- that has Associated Series $targetSeries
		There is an administered shot $administeredShotContainsNonPertussis
			- the shot belongs to the series $targetSeries
			/////// - that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the diseases targeted by the vaccine administered as $diseasesTargeted
			- the collection $diseasesTargeted does not contain "SUPPORTED_DISEASE_CONCEPT.033.9"
			- make note of the date this shot was administered as $dtAdministrationDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			/////// - that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedOther
			- the collection $diseasesTargetedOther does not contain "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the administration date of the shot is > $dtAdministrationDate
	then
		// Recommendation at 0 days from the last non-pertussis dose in the series
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Recommended Forecast Date for $oRecommendationInterval to $dtAdministrationDate
		Set the Recommendation Earliest Forecast Date for $oRecommendationInterval to $dtAdministrationDate
		Set the Recommendation Vaccine for $oRecommendationInterval to "ICE115"
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "DTP: If either the 3-Dose or 5-Dose Series is Complete and an Adolescent Tdap is required, recommend Tdap at earliest & recommended interval of 6 months from last pertussis dose" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
			- that has Associated Series $targetSeries
		There is an administered shot $administeredShotContainsPertussis
			- the shot belongs to the Series $targetSeries
			- make note of the diseases targeted by the vaccine administered as $diseasesTargeted
			- the collection $diseasesTargeted contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- make note of the date this shot was administered as $dtAdministrationDate
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedOther
			- the collection $diseasesTargetedOther contains "SUPPORTED_DISEASE_CONCEPT.033.9"
			- the administration date of the shot is > $dtAdministrationDate			
	then
		// Recommendation at 6 months from the last shot in the series
		Add 6 DurationType.MONTHS to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Recommended Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Earliest Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval to "ICE115"
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/**************************************************************************************************************************************************************************************************************************************
 Recommendation Rules for Adolescent Tdap END
/*************************************************************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Rules for Recurring Td (CVX 09)
Once the patient has completed the primary DTP series (3-dose or 5-dose) and has received a dose of pertussis at >= 7 years of age, a Td (09) is needed every 10 years.  

Evaluation Rules for Recurring Td
    + All vaccines in the DTP vaccine group are allowed for the recurring Td.
    + Minimum interval = 0 days 
    + Once the primary series is complete and the patient has a dose of pertussis at >=7, any DTP shot is evaluated as VALID for the recurring Td.
*************************************************************************************************************************************************************************************/

// If a patient has completed the Adolescent Tdap requirements, then the next dose in the series should be recommended as Td (CVX 09).
rule "DTP: Recommend Td (CVX 09) at earliest interval of 5y, recommended interval of 10yrs, and latest recommended interval of 10y+4w if series is complete has received a pertussis dose >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationIntervalCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the Series is complete
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_COMPLETED.conceptCodeValue
			- that has Associated Series $targetSeries
	then
		Add "5y" to $dtAdministrationDate and make note of the newly calculated date as $dtEarliestRecommendedCalculated
		Add "10y" to $dtAdministrationDate and make note of the newly calculated date as $dtRecommendedCalculated
		Add "10y+4w" to $dtAdministrationDate and make note of the newly calculated date as $dtOverdueCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Recommended Forecast Date for $oRecommendationInterval to $dtRecommendedCalculated
		Set the Recommendation Earliest Forecast Date for $oRecommendationInterval to $dtEarliestRecommendedCalculated
		Set the Recommendation Overdue Forecast Date for $oRecommendationInterval to $dtOverdueCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval to "ICE09"
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
Recurring Rules for Td END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
5-Dose Series Special Rules 
Rule for Recommending/Evaluating Next Dose From Invalid Tdap/TD Given Below Minimum Age for Vaccine
If Tdap (CVX 115) or Td (CVX 09, 113, 138, 139) is given as target dose 1, 2 or 3 to a patient < 7yrs-4days old (vaccine minimum age), then the invalid shot should be ignored 
when determining the recommended due date for the next target dose due
*************************************************************************************************************************************************************************************/

// Nothing to do. Shots marked ignored during evaluation stage will be ignored by base rules when calculating recommendation intervals.

/*************************************************************************************************************************************************************************************
5-Dose Series Special Rules  END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
5-dose Series Exception Rules
The 5-dose series above applies unless:

Exception 1: Series Complete with 3 Doses
    * If the patient is >=7 or will be >=7 as of the next due date, received the first dose at >= 12 months of age, and received at least one dose at >= 4 years of age, then the 
      series is complete with 3 doses (doses 2, 3 and 4).
*************************************************************************************************************************************************************************************/

rule "DTP: (5-Dose Series Exception Rule 1)- Mark the 5-Dose Series Complete for Patient who will be >= 7yrs as of the next due date, if 3 doses have been administered with the first dose at >= 12m and at least 1 dose at >= 4yrs" 
 	ruleflow-group "RecommendationForecast"
 	agenda-group "postRecommendationCheck"
 	dialect "mvel"
 	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the series is "DTP5DoseSeries"
			- the number of doses administered is == 3
			- make note of the Recommendation Date as $recommendationDate
			- post processing on the Series forecast has not already been run
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
			- make note of the date as $dtDateAtAge4y when the patient is "4y" of age
			- make note of the date as $dtDateAtAge12m when the patient is "12m" of age
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge12m
			- the Dose Number in the series is == 1
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge4y
			- the Dose Number in the series is >= 2
		Confirm elapsed time between $birthDate and $recommendationDate >= "7y" 	
 	then
		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for forecasting
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

rule "DTP: (5-Dose Series Exception Rule 1)- Skip to Dose 4 if patient >= 7yrs and has had 2 doses with the first dose at >= 12m and one dose at >= 4 yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	no-loop true
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP5DoseSeries"
			- the number of doses administered is == 2
			- make note of the effective dose number in the Series as $nEffectiveDoseNumber
			- the numeric $nEffectiveDoseNumber is == 3
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAge4y when the patient is "4y" of age
			- make note of the date as $dtDateAtAge12m when the Patient is "12m" of age
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge12m
			- the Dose Number in the series is == 1
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge4y
			- the Dose Number in the series is >= 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "7y"
	then
		Skip Series Dose Number to 4 from $nEffectiveDoseNumber for all diseases in the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end 

/*************************************************************************************************************************************************************************************
5-Dose Series Exception Rules  END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
 Six by Seven Rule: If a child is < 7 years of age has received 6 shots from the DTP vaccine group, but has not yet completed the series, then the next dose is recommended at age 
 7 years; follow table rules for to determine the recommended interval.
*************************************************************************************************************************************************************************************/

rule "DTP: (Six by Seven Rule)- Recommend next dose at age 7yrs for 5-dose series if patient < 7yrs, series is not complete and has received 6 shots" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	dialect "mvel"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP5DoseSeries"
			- the Series is not complete
			- the number of administered shots excluding duplicate shots on the same day is >= 6
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		Confirm elapsed time between $birthdate and evalTime < "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 3-dose Series Exception Rules START:
    + THE FOLLOWING RULES ARE IMPLEMENTED in Evaluation^DTP: If the patient has received 3 doses and none of the doses are pertussis-containing vaccines, the DTP 3-dose series is NOT complete. 
    + RECOMMENDATION RULE TO IMPLEMENT HER: Recommend Tdap (CVX 115) at recommended interval = 0 days. 
 Example: Patient age = 10 years. CVX 09 (Td) given at 7 years, 8 years, and 10 years. Recommend final dose Tdap (CVX 115) at 10 years. 
/************************************************************************************************************************************************************************************/

rule "DTP: (3-Dose Series Exception Rule[Recommendation])- If none of the 3 or more doses administered in 3-dose Series contains pertussis, recommend Tdap in 0 days following the most recent dose" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.200"
			- the name of the Series is "DTP3DoseSeries"
			- the Series is not complete
			- the number of Doses administered is >= 3
		There does not exist an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- that has Associated Series $targetSeries
		There is an Administered Shot $latestAdministeredShotIn3DoseSeries
			- the Shot belongs to the Series $targetSeries
			- make note of the Administered Shot Number as $administeredShotNumber
			- make note of the date this Shot was Administered as $dateLastShotAdministered
		There does not exist an Administered Shot
			- the Shot belongs to the Series $targetSeries
			- the Administered Shot Number is > $administeredShotNumber
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Recommended Forecast Date for $oRecommendation to $dateLastShotAdministered
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		/////// Vaccine-specific forecast rules determine that a Tdap should be recommended; so specific recommendation of Tdap vaccine does not need to be specified here 
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 3-Dose Series Exception Rules END
/************************************************************************************************************************************************************************************/
