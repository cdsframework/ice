/**
 * Copyright (C) 2024 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.Date
import java.util.Iterator
import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.concepts.ImmunizationConcept
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.opencds.vmr.v1_0.internal.SubstanceAdministrationEvent
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.InconsistentConfigurationException
import org.cdsframework.ice.service.Season
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeasons
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType


global java.util.Date evalTime
global org.cdsframework.ice.service.Schedule schedule


/*************************************************************************************************************************************************************************************
 Influenza TargetSeries Initialization Rules
*************************************************************************************************************************************************************************************/

rule "CandidateSeriesIdentification (Influenza): Initialize TargetSeries for *CURRENT* *Influenza* Season with Most Recent Prior Season Rules if Current Season Not Defined but Prior Season is Defined" 
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule"
	salience 10
	when
		$s : SeriesRules($vg : vaccineGroup, $sseriesName : seriesName, vaccineGroup == "VACCINE_GROUP_CONCEPT.800", seasons.size() > 0)
		not Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, dateIsApplicableToSeason(evalTime))
		$priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, $priorSeasonStart : fullySpecifiedSeasonStartDate, $priorSeasonStart.toDate() < evalTime)
		Number(intValue > 0) from accumulate($accseason : Season(this == $priorSeason) from $s.seasons, count($accseason))
		not Season(vaccineGroup == $s.vaccineGroup, isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, fullySpecifiedSeasonStartDate.isAfter($priorSeason.fullySpecifiedSeasonStartDate))
		not (TargetSeries(seriesName == $s.seriesName, vaccineGroup == $s.vaccineGroup, targetSeason != null, targetSeason == 
			Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, $priorSeason.seasonStartMonth, $priorSeason.seasonStartDay, $priorSeason.seasonEndMonth, $priorSeason.seasonEndDay), evalTime)))
		$tseasons : TargetSeasons()
		$evalutedPerson : EvaluatedPerson()
	then
		String _RULENAME = kcontext.rule.name;
		int startMonth = $priorSeason.getSeasonStartMonth();
		int startDay = $priorSeason.getSeasonStartDay();
		int endMonth = $priorSeason.getSeasonEndMonth();
		int endDay = $priorSeason.getSeasonEndDay();
		Season fullySpecified = Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, startMonth, startDay, endMonth, endDay), evalTime);
		$tseasons.addTargetSeason(fullySpecified);
		TargetSeries ts = new TargetSeries($s, schedule, fullySpecified, $evalutedPerson);
		$s.addFullySpecifiedSeason(fullySpecified);
		insert(ts);
		insert(fullySpecified);
		update($s);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Seasonal* TargetSeries initialized: " + ts.toString());
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Updated* TargetSeasons: " + $tseasons.toString());
end


rule "CandidateSeriesIdentification (Influenza): Initialize TargetSeries for *Current* or *Past* *Influenza* Season with Shots Administered with Most Recent Prior Season if No Applicable Season Defined at Time Shot Administered" 
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule"
	when
		ImmunizationConcept($ctid : conceptTargetId, $occ : openCdsConceptCode)
		SubstanceAdministrationEvent(id == $ctid, $shotDate : ICELogicHelper.extractSingularDateValueFromIVLDate(administrationTimeInterval))
		$s : SeriesRules($sseriesName : seriesName, vaccineGroup == "VACCINE_GROUP_CONCEPT.800", seasons.size() > 0)
		$priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, $priorSeasonStart : fullySpecifiedSeasonStartDate, $priorSeasonStart.toDate() < $shotDate)
		Number(intValue > 0) from accumulate($accseason : Season(this == $priorSeason) from $s.seasons, count($accseason))
		not Season(vaccineGroup == $s.vaccineGroup, isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, fullySpecifiedSeasonStartDate.isAfter($priorSeason.fullySpecifiedSeasonStartDate))
		not (TargetSeries(seriesName == $s.seriesName, vaccineGroup == $s.vaccineGroup, targetSeason != null, targetSeason == 
				Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, $priorSeason.seasonStartMonth, $priorSeason.seasonStartDay, $priorSeason.seasonEndMonth, $priorSeason.seasonEndDay), $shotDate)))
		$tseasons : TargetSeasons()
		eval(schedule.vaccineTargetsOneOrMoreOfSpecifiedDiseases(schedule.getVaccineByCdsConceptValue($occ), schedule.getDiseasesTargetedByVaccineGroup($s.vaccineGroup)) == true)
		$evaluatedPerson : EvaluatedPerson()
	then
		String _RULENAME = kcontext.rule.name;
		int startMonth = $priorSeason.getSeasonStartMonth();
		int startDay = $priorSeason.getSeasonStartDay();
		int endMonth = $priorSeason.getSeasonEndMonth();
		int endDay = $priorSeason.getSeasonEndDay();
		Season fullySpecified = Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, startMonth, startDay, endMonth, endDay), $shotDate);
		$tseasons.addTargetSeason(fullySpecified);
		TargetSeries ts = new TargetSeries($s, schedule, fullySpecified, $evaluatedPerson);
		$s.addFullySpecifiedSeason(fullySpecified);
		insert(ts);
		insert(fullySpecified);
		update($s);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Seasonal* TargetSeries initialized: " + ts.toString());
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Updated* TargetSeasons: " + $tseasons.toString());	
end


/*************************************************************************************************************************************************************************************
 RSV TargetSeries Initialization Rules
*************************************************************************************************************************************************************************************/

/////////////////////
// Logic to either select or combine the infant and/or adult series based on the current age of the patient and the age at which shots were administered. 
// This logic will be generalized and extended for use with other vaccine groups (such as pneumococcal) at a later time. This initial implementation for RSV, all of which can be 
// implemented in this agenda group, is as follows:  
//   + If there are no shots administered and the patient age is <= the maxAge of the infant series, add the infant series. Do not add the adult series.
//   + If there are no shots administered and the patient age is >= the minAge of the adult series, add the adult series. Do not add the infant series.
//   + If there are shots administered but they are all <= the max age of the infant series, simply add the infant series; do not add the adult series
//   + If there are shots administered but they are all >= the min age of the adult series, simply add the adult series; do not add the infant series
//   + If there are shots administered both <= the maxAge of the infant series and >= the minAge of the adult series, combine them.
/////////////////////


///////
// RSV CandidateSeriesIdentification Initial Checks and Workflow
///////

rule "CandidateSeriesIdentification(RSV): If there are exactly 2 RSV series whereby the Infant Series has a patientEndAge that is 1 day less than the patientStartAge of the Adult Series, invoke series identification workflow"
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule"
	when
		accumulate($srrsv : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875"); $count : count($srrsv); $count == 2)
		$rsvinfant : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVInfantSeries")
		$rsvadult : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVAdultSeries")
		eval($rsvinfant.seriesEndAge != null && $rsvadult.seriesStartAge != null && TimePeriod.differenceInDays(TimePeriod.addTimePeriod(evalTime, $rsvinfant.seriesEndAge), TimePeriod.addTimePeriod(evalTime, $rsvadult.seriesStartAge)) == 1)
	then
		String _RULENAME = kcontext.rule.name;
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "Criteria of exactly 2 RSV series defined as required; RSV series processing to continue");
		// setFocus("CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV");
		setFocus("CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV");
end


rule "CandidateSeriesIdentification(RSV): Prevent the default CandidateSeriesIdentification rules from running on the RSV Adult Series"
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule"
	when
		$s : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVAdultSeries")
		not ICEFactTypeFinding(iceResultFinding == SupportedFactConcept._CANDIDATE_SERIES_IDENTIFICATION_DEFAULT_RULE_OVERRIDE.conceptCodeValue, associatedSeriesRules == $s)
	then
		String _RULENAME = kcontext.rule.name;
		ICEFactTypeFinding iftf = new ICEFactTypeFinding(SupportedFactConcept._CANDIDATE_SERIES_IDENTIFICATION_DEFAULT_RULE_OVERRIDE.conceptCodeValue, $s);
		insert(iftf);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "SeriesRules \"" + $s.seriesName + "\" will not be examined by default CandidateSeriesIdentification rules");
end


///////
// RSV Adult TargetSeries Initializations
///////

rule "CandidateSeriesIdentification(RSV): Initialize an Adult RSV TargetSeries if the patient age is >= the Series Start Age"
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV"
	salience 10
	when
		$s : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVAdultSeries", seasons.isEmpty(), seriesStartAge != null)
		not TargetSeries(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == $s.seriesName)
		EvaluatedPerson($birthDate : demographics.birthTime)
		eval(TimePeriod.compareElapsedTimePeriodToDateRange($birthDate, evalTime, $s.seriesStartAge) >= 0)
		$evaluatedPerson : EvaluatedPerson()
	then
		String _RULENAME = kcontext.rule.name;
		TargetSeries ts = new TargetSeries($s, schedule, $evaluatedPerson);
		insert(ts);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "TargetSeries initialized: " + ts.toString());
end


rule "CandidateSeriesIdentification(RSV): Initialize an Adult RSV TargetSeries if there is a shot administered is >= the Series Start Age"
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV"
	when
		$s : SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVAdultSeries", seasons.isEmpty(), seriesStartAge != null)
		not TargetSeries(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == $s.seriesName)
		ImmunizationConcept($ctid : conceptTargetId, $occ : openCdsConceptCode)
		SubstanceAdministrationEvent(id == $ctid, $shotDate : ICELogicHelper.extractSingularDateValueFromIVLDate(administrationTimeInterval))
		eval(schedule.vaccineTargetsOneOrMoreOfSpecifiedDiseases(schedule.getVaccineByCdsConceptValue($occ), schedule.getDiseasesTargetedByVaccineGroup($s.vaccineGroup)) == true)	
		EvaluatedPerson($birthDate : demographics.birthTime)
		eval(TimePeriod.compareElapsedTimePeriodToDateRange($birthDate, $shotDate, $s.seriesStartAge) >= 0)
		$evaluatedPerson : EvaluatedPerson()
	then
		String _RULENAME = kcontext.rule.name;
		TargetSeries ts = new TargetSeries($s, schedule, $evaluatedPerson);
		insert(ts);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "TargetSeries initialized: " + ts.toString());
end


rule "CandidateSeriesIdentification(RSV): If the patient age is <= the RSV Infant Series End Age, initialize a TargetSeries for *Current* *RSV* Season with Most Recent Prior Season Rules if Current Season Not Defined but Prior Season is Defined" 
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV"
	salience 190
	when
		$s: SeriesRules(vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seriesName == "RSVInfantSeries", seasons.size() > 0, seriesEndAge != null)
		not Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, dateIsApplicableToSeason(evalTime))
		/////// $priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup)
		$priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, $priorSeasonStart : fullySpecifiedSeasonStartDate, $priorSeasonStart.toDate() < evalTime)
		Number(intValue > 0) from accumulate($accseason : Season(this == $priorSeason) from $s.seasons, count($accseason))
		not Season(vaccineGroup == $s.vaccineGroup, isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, fullySpecifiedSeasonStartDate.isAfter($priorSeason.fullySpecifiedSeasonStartDate))
		not (TargetSeries(seriesName == $s.seriesName, vaccineGroup == $s.vaccineGroup, targetSeason != null, targetSeason == 
			Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, $priorSeason.seasonStartMonth, $priorSeason.seasonStartDay, $priorSeason.seasonEndMonth, $priorSeason.seasonEndDay), evalTime)))
		$tseasons : TargetSeasons()
		$evaluatedPerson : EvaluatedPerson($birthDate : demographics.birthTime)
		eval(TimePeriod.compareElapsedTimePeriodToDateRange($birthDate, evalTime, new TimePeriod($s.seriesEndAge.getTimePeriodStringRepresentation() + "+1d")) < 0)
	then
		String _RULENAME = kcontext.rule.name;
		int startMonth = $priorSeason.getSeasonStartMonth();
		int startDay = $priorSeason.getSeasonStartDay();
		int endMonth = $priorSeason.getSeasonEndMonth();
		int endDay = $priorSeason.getSeasonEndDay();
		Season fullySpecified = Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, startMonth, startDay, endMonth, endDay), evalTime);
		$tseasons.addTargetSeason(fullySpecified);
		TargetSeries ts = new TargetSeries($s, schedule, fullySpecified, $evaluatedPerson);
		$s.addFullySpecifiedSeason(fullySpecified);
		insert(ts);
		insert(fullySpecified);
		update($s);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Seasonal* TargetSeries initialized: " + ts.toString() + "; *Updated* TargetSeasons: " + $tseasons.toString());
end


rule "CandidateSeriesIdentification(RSV): If a shot was administered <= the RSV Infant Series End Age, initialize a TargetSeries for *Current* or *Past* *RSV* Season if Shots were Administered using with Most Recent Prior Season if No Applicable Season Defined at Time Shot was Administered" 
	dialect "mvel"
	agenda-group "CandidateSeriesIdentification^customCandidateSeriesIdentificationRule^RSV"
	salience 180
	when
		ImmunizationConcept($ctid : conceptTargetId, $occ : openCdsConceptCode)
		SubstanceAdministrationEvent(id == $ctid, $shotDate : ICELogicHelper.extractSingularDateValueFromIVLDate(administrationTimeInterval))
		$s : SeriesRules($sseriesName : seriesName, vaccineGroup == "VACCINE_GROUP_CONCEPT.875", seasons.size() > 0)
		/////// $priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup)
		$priorSeason : Season(isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, vaccineGroup == $s.vaccineGroup, $priorSeasonStart : fullySpecifiedSeasonStartDate, $priorSeasonStart.toDate() < $shotDate)
		Number(intValue > 0) from accumulate($accseason : Season(this == $priorSeason) from $s.seasons, count($accseason))
		not Season(vaccineGroup == $s.vaccineGroup, isDefaultSeason() == false, isDefinedBySeriesTableRules() == true, fullySpecifiedSeasonStartDate.isAfter($priorSeason.fullySpecifiedSeasonStartDate))
		not (TargetSeries(seriesName == $s.seriesName, vaccineGroup == $s.vaccineGroup, targetSeason != null, targetSeason == 
				Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, $priorSeason.seasonStartMonth, $priorSeason.seasonStartDay, $priorSeason.seasonEndMonth, $priorSeason.seasonEndDay), $shotDate)))
		eval(schedule.vaccineTargetsOneOrMoreOfSpecifiedDiseases(schedule.getVaccineByCdsConceptValue($occ), schedule.getDiseasesTargetedByVaccineGroup($s.vaccineGroup)) == true)
		$evaluatedPerson : EvaluatedPerson($birthDate : demographics.birthTime)
		eval(TimePeriod.compareElapsedTimePeriodToDateRange($birthDate, $shotDate, new TimePeriod($s.seriesEndAge.getTimePeriodStringRepresentation() + "+1d")) < 0)
		$tseasons : TargetSeasons()
	then
		String _RULENAME = kcontext.rule.name;
		int startMonth = $priorSeason.getSeasonStartMonth();
		int startDay = $priorSeason.getSeasonStartDay();
		int endMonth = $priorSeason.getSeasonEndMonth();
		int endDay = $priorSeason.getSeasonEndDay();
		Season fullySpecified = Season.constructFullySpecifiedSeasonFromDefaultSeasonAndDate(new Season($priorSeason.seasonName, $s.vaccineGroup, false, startMonth, startDay, endMonth, endDay), $shotDate);
		$tseasons.addTargetSeason(fullySpecified);
		TargetSeries ts = new TargetSeries($s, schedule, fullySpecified, $evaluatedPerson);
		$s.addFullySpecifiedSeason(fullySpecified);
		insert(ts);
		insert(fullySpecified);
		update($s);
		ICELogicHelper.logDRLDebugMessage(_RULENAME, "*Seasonal* TargetSeries initialized: " + ts.toString() + "; *Updated* TargetSeasons: " + $tseasons.toString());
end
