/**
 * Copyright (C) 2024 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime


rule "Meningococcal: Mark the Series as Complete Before Evaluation of Shot if Prior Dose 1 is Administered >= 16yrs and < 19yrs of Age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an Administered Shot $dose1
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.830" and the Series with name "MCV42DoseSeries"
			- the Dose Number in the Series is == 1
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the date this shot was administered as $dtAdministrationDateOne
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAge when the patient is "16y" of age				
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.830"
			- the name of the Series is "MCV42DoseSeries"
			- the Series is Not Complete
			- the number of Doses Administered is == 1
			- the number of Doses Administered before $dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDateOne >= "16y" && elapsed time between $dtBirthDate and $dtAdministrationDateOne < "19y"			
	then
		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Meningococcal: Evaluate shot as Accepted/Below Recommended Age and does not count towards series completion if given below the minimum age for the series and above the vaccine minimum age; skip base rules"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.830"
			- the dose number in the series is == 1
			- make note of the date this shot was administered as $shotDate
			- make note of the minimum vaccine age for this shot as $validMinAgeVaccine
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.830"
			- make note of the Absolute Minimum Age for Dose 1 in this Series as $minAgeDoseOne
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $shotDate < $minAgeDoseOne && elapsed time between $dtBirthDate and $shotDate >= $validMinAgeVaccine
	then
		Mark the shot $currentShot as Ignored
		Include the reason for shot $currentShot Accepted due to "Below Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Meningococcal: Do not run base rules for minimum age for vaccine if given below the minimum age for the series and above the vaccine minimum age" 
		extends	"Meningococcal: Evaluate shot as Accepted/Below Recommended Age and does not count towards series completion if given below the minimum age for the series and above the vaccine minimum age; skip base rules"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		// Nothing to do
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Meningococcal: Skip age evaluation of the Shot if given below the minimum age for the series and below the vaccine minimum age; skip age base rules"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.830"
			- make note of the date this shot was administered as $shotDate
			- make note of the minimum vaccine age for this shot as $validMinAgeVaccine
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $shotDate < $validMinAgeVaccine 
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "Evaluate Shot as Accepted & Above Recommended Age if age at administration >= 22yrs and has not completed series < 22yrs of age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
    when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.830"
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
			- make note of the Associated Series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "19y" of age
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.830"
			- the Series is not Complete
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDateCurrentDose >= "22y"
    then
		Include the reason for shot $currentShot Accepted due to "Above Recommended Age for Series"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
