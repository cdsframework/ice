/**
 * Copyright (C) 2019 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
 package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


/*************************************************************************************************************************************************************************************
Series complete with 3 doses: If patient is >= 4 years old (or will be >= 4 years old at the dose 3 recommended due date), recommend target dose 3 (and final dose) at 4 years old; 
minimum interval = 6 months (for earliest date); recommended interval = 6 months.
*************************************************************************************************************************************************************************************/

// Series complete with 3 doses if dose 3 was administered when the patient was >= 4 years and was administered at least 6 months - 4 days after the previous dose, then the series is complete with 3 doses.
rule "Polio: (Series Complete with 3 Doses Rule 2)- Series is Complete if 3 prior doses, patient >= 4yrs when dose 3 administered, and dose 3 >= 6m-4d after previous dose" ruleflow-group "HistoryEvaluation" 
	dialect "mvel"
	agenda-group "postEvaluationCheck"
	when
		There is an Administered Shot $dose3
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- that has already been evaluated and whose Shot Validity is VALID
			- the dose number in the series is == 3
			- make note of the date this shot was administered as $dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		There is an Administered Shot $prevdose2
			- the shot belongs to the Series $associatedTargetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the dose number in the series is == 2
			- make note of the date this shot was administered as $dtAdministrationDatePrev
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the series is not complete
			- the number of doses administered is == 3
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDate >= "4y" && elapsed time between $dtAdministrationDatePrev and $dtAdministrationDate >= "6m-4d"
	then
		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
Series complete with 3 doses END
*************************************************************************************************************************************************************************************/

rule "Polio: Modify DoseRule4 Absolute minimum age and Absolute Minimum Interval parameters for target dose 4 if administered before 8/7/2009" ruleflow-group "HistoryEvaluation"
	agenda-group "customEvaluationRule"
	no-loop true
	when
		There is an Administered Shot $currentDose that needs to be evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400" and the Series with name "Polio4DoseSeries"
			- the dose number in the series is == 4
			- the administration date of the shot is < "07-Aug-2009"
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
			- make note of the Associated Series as $associatedTargetSeries
	then
		Obtain the existing DoseRule for Dose Number 4 in the Series $associatedTargetSeries as $assign_oDoseRule4
		Set the Absolute Minimum Age for DoseRule $assign_oDoseRule4 to TimePeriod "122d"
		Replace the existing DoseRule in the Series $associatedTargetSeries with $assign_oDoseRule4
		Obtain the existing DoseRule for Dose Number 3 in the Series $associatedTargetSeries as $assign_oDoseRule3
		Set the Absolute Minimum Interval for DoseRule $assign_oDoseRule3 to TimePeriod "24d"
		Replace the existing DoseRule in the Series $associatedTargetSeries with $assign_oDoseRule3
		Refresh all Facts in the Series $associatedTargetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $associatedTargetSeries
		Log that this Series Rule fired for the Series $associatedTargetSeries
end


// For children who received target dose 4 at >= 8/7/2009 and before 4 years - 4 days of age, evaluate target dose 4 as Accepted/ BELOW_MINIMUM_AGE_FINAL_DOSE 
// (if absolute minimum interval is met). (An additional dose will be recommended at >= 4 years of age).
rule "Polio: Mark target dose 4 administered >= 8/7/2009 and before absolute minimum age as ACCEPTED with reason BELOW_MINIMUM_AGE_FINAL_DOSE" ruleflow-group "HistoryEvaluation"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $targetDose4 that needs to be evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400" and the Series with name "Polio4DoseSeries"
			- the dose number in the series is == 4
			- the administration date of the shot is >= "07-Aug-2009"
			- make note of the date this shot was administered as $dtAdministrationDate
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the name of the Series is "Polio4DoseSeries"
			- make note of the absolute minimum age for dose 4 in this series as $absoluteMinimumAge
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDate < $absoluteMinimumAge
	then
		Include the reason for shot $targetDose4 Accepted due to "Below Minimum Age for Final Dose"
		Record that this dose rule was Processed for the TargetDose $targetDose4
        Log that this dose rule fired for the dose $targetDose4 in the Series $targetSeries
end


// If a patient receives CVX 178 or CVX 179, evaluate these shots as Invalid with reason code MISSING_ANTIGEN
rule "Polio: Mark OPV shots (CVX 178 or CVX 179) as INVALID with reason MISSING_ANTIGEN" ruleflow-group "HistoryEvaluation"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the vaccine administered a member of ("ICE178","ICE179")
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the Series is Not Complete
	then
		Include the reason for shot $currentShot Invalid due to "Missing Antigen"
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// If a patient receives shots of OPV administered >= 4/1/2016, these shots do not count towards the series. Evaluate these shots as Invalid with reason code MISSING_ANTIGEN
rule "Polio: Mark OPV shots administered >= 4/1/2016 as INVALID with reason MISSING_ANTIGEN" ruleflow-group "HistoryEvaluation"
	agenda-group "customEvaluationRule"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the Shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the administration date of the shot is >= "01-Apr-2016"
			- the vaccine administered a member of ("ICE02","ICE178","ICE179","ICE182")
			- make note of the Associated Series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.400"
			- the Series is Not Complete
	then
		Include the reason for shot $currentShot Invalid due to "Missing Antigen"
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
